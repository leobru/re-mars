шифр 419900зс5^
лен 67(2345-зп)^
ацпу 128^
еев1а3
*name
*bemsh
inp$$$
 МАСRО
 ЭКВИВ ∧А,∧Б,∧В,∧Г,∧Д,∧Е,∧Ж,∧З,∧И,∧Й,∧К,∧Л,∧Н,∧О,∧П,∧Р
∧А(1) ЭКВ ∧А(2)
 АIF (∧Б ЕQ ’’).ВСЕ
 ЭКВИВ ∧Б,∧В,∧Г,∧Д,∧Е,∧Ж,∧З,∧И,∧Й,∧К,∧Л,∧Н,∧О,∧П,∧Р
.ВСЕ МЕND
        macro
&l      переп    &a,&b
&l	мод	&a
	сч	-1(М16)
	мод	&b
	зп	-1(М16)
	слиа	-1(М16)
        mend
        macro
&l      пио     &a,&b
&l      мода    &a
        vzm     (&b)
        mend
        macro
&l      пино     &a,&b
&l      мода    &a
        v1m     (&b)
        mend
        macro
&l      пв     &a,&b
&l      мода    &a
        vjm     (&b)
        mend
бандит СТАРТ '2000'
m4 equ 4
m13 equ 11
m5 equ 5
m6 equ 6
m7 equ 7
m16 equ 14
	употр бандит(М4)
	конд в'0'
	конд п'бандит'
START	счи	М16
	пб	enter
abdv	пам	1
arch	конд	в'1550063'      каталог МАРС по умолчанию
	счи	М16
	зп	savm16
	пб	enter2
savm16	пам	1
D00010	конд	п'190280'       дата версии, потом затирается
D00011	пам	1
D00012	пам	1
savm13	пам	1
goto	пам	1
D00015	пам	1
usrloc	пам	1
savm7	пам	1
savm6	пам	1
savm5	пам	1
jmpoff	пам	1
D00023	пам	1
D00024	пам	1
D00025	пам	1
D00026	пам	1
temp	пам	1
D00030	пам	1
D00031	пам	1
D00032	пам	1
D00033	пам	1
D00034	пам	1
work	пам	1
work2	пам	1
ИСЭ70	пам	1
D00040	пам	1
savrk	пам	1
* Все команды приходят на дешифровщик с 0 на См.
switch	пб	cmd0
        зп
* Команда 01: установка текущей записи на начало
k01	зп	temp
	пб	cmd1
* Команда 02: установка текущей записи на конец
k02	сч	=М47В'1'В'-1'
	пб	find
* Команда 03: шаг назад
k03	сч	=В'1'
	зп	
* Команда 04: шаг вперед
k04	уи	М16
	пб	cmd4
k05	зп	D00011
	пб	cmd5
k06	зп	idx(М13)
	пб	cmd6
k07	сч	=a(a00317-избазы)
	пб	cmd7
* Команда 10: создать базу/каталог; сама потом выполняет 21
k10	зп	which(М13)
	пб	mkctl
* Команда 11: поиск элемента по ключу
k11	сч	key(М13)
	пб	find
* Команда 12: скопировать текущий элемент в дескриптор базы
k12	счи	М13
	пб	cmd12
k13	зп	itemln(М13)
	пб	pr425
* Команда 14: проверка совпадения ключей
* Если команда последняя или за ней не 00, то неудача - ошибка,
* иначе пропустить 00 и следующие 3 команды
k14	сч	curkey(М13)
	пб	ckexst
* Команда 15: проверка несовпадения ключей; аналогично 14
k15	сч	curkey(М13)
	пб	ckdnex
* Команда 16: поиск символа конца строки
k16	вч	mylen(М13)
	пб	fnendc
* Команда 17: поиск слова-признака конца
k17	вч	mylen(М13)
	пб	fnendw
* Команда 20: копирование из памяти в текущий элемент
k20	сч	myloc(М13)
	пб	cmd20
* Команда 21: сохранение буфера на диск        
k21	сч	myloc(М13)
	пб	flush
* Команда 22: копирование текущего элемента в память пользователя
k22	сч	adescr(М13)
	пб	cpyout
* Команда 23: освобождение памяти элемента
k23	сч	adescr(М13)
	пб	free
* Команда 24: проверка "пароля"
k24	сч	givenp(М13)
	пб	chkpsw
* Команда 25: установить текущую базу по дескриптору
k25	сч	dbdesc(М13)
	пб	setctl
k26	сч	key(М13)
	пб	cmd26
k27	зп	loc36(М13)
	пб	cmd27
k30	сч	loc3(M13)
	пб	next
* Команда 31: установить корневой каталог в качестве текущего
k31	сч	=М11В'1'
	пб	root
k32	сч	loc12(М13)
	пб	cmd32
k33	сч	adescr(М13)
	пб	pr342
k34	сч	loc41(М13)
	пб	cmd34
* Команда 35: запись буферов на диск
k35	сч	dirty(М13)
	пб	save
k36	сч	loc12(М13)
	пб	cmd36
* Команда 37: пропустить следующую команду
k37	сч	curcmd(М13)
	сда	64+12
* Команда 40: останов, нормальное завершение
k40	пб	next
k41	сч	=a(сравн-избазы)
	пб	k43
k42	сч	=a(вбазу-избазы)
k43	зп	jmpoff
	пб	cmd43
* Команда 44: adescr := loc41(М13)
k44	сч	loc41(М13)
	пб	cmd44
* Команда 45: установить флаг работы с базой
k45	сч	bdtab(М13)
	пб	lock
k46	сч	myloc(М13)
	пб	cmd46
* Команда 47: уход на первую ячейку bdvect
k47	сч	loc41(М13)
	пб	outadr(M13)
* Команда 50 хх: cmd := bdvect[bdvect[xx]++]
k50	сч	=В'3'
	пб	cmd50
* Команда 51 хх: myloc := bdvect[bdvect[xx]++]
k51	сч	=В'13'
	пб	cmd51
* Команда 52 xx yy: bdvect[xx] := bdvect[bdvect[yy]++]
k52	сч	curcmd(М13)
	пб	cmd52
* Команда 53 xx yy: bdvect[xx] := bdvect[yy]
k53	сч	curcmd(М13)
	пб	move
* Команда 54 xx: bdvect[xx] := bdvect[012]
k54	сч	curcmd(М13)
	пб	cmd54
* Команда 55: немедленный возврат
k55	мод	savm16
	пб	
********************
cmd32	мод	aitem(M13)
	зп	
	сч	which(М13)
	по	a00123
	сч	=В'2'
a00123	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пб	(М6)
********************
enter	зп	savm16
	счи	М13
	зп	savm13
	счрж	'7777'
	зп	savrk
	ржа	'3'
	сч	abdv
	уи	М13
	счи	М7
	зп	savm7
	счи	М5
	зп	savm5
	мод	bdtab(M13)
	сч	
	нтж	rootfl(М13)
	по	a00141
	сч	e70msk(М13)
	по	a00141
	сч	bdtab(М13)
	сда	64-20
	или	=М39В'1'
	или	e70msk(М13)
	зп	ИСЭ70
	Э70	ИСЭ70
a00141	счи	М6
	зп	savm6
enter2	сч	loc3(M13)
	пб	next
********************
a00143	сч	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
a00145	мод	goto
	уиа	0(М5)
	сч	
	зп	goto
	пино	(М5),М5
cmd0	сч	curcmd(М13)
	сда	64+6
next	по	a00626
	зп	curcmd(М13)
	и	=В'77'
	уи	М5
A00153	мода	cmd0
	уиа	0(М6)
	сч	
	зп	goto
	мода	(М5)
	пб	switch
********************
find	зп	temp
	по	badnam          ошибка НЕДОП. ИМЯ.
	и	=М47В'1'
	пе	badnam          ---""---
cmd1	зп	idx(М13)
	сч	temp
	нтж	=М47В'1'В'-1'
	зп	temp
cmd1a	мода	loc56(M13)
	уиа	0(М16)
	сч	loc20(М13)
	по	a00212
A00164	сч	-1(М16)
	и	=В'1777'
	уи	М5
	по	a00172
A00166	мода	(М5)
	сч	-2(М16)
	слц	temp
	пе	a00172
	слиа	-2(М5)
	пино	A00166,М5
a00172	слиа	-2(М5)
	счи	М5
	или	=М30В'1'
	мод	idx(М13)
	зп	array(М13)
	сч	1(М16)
	и	=М47В'1'
	по	a00371
	сч	idx(М13)
	слц	=В'2'
	зп	idx(М13)
	мода	(М16)
	сч	1(М5)
	мода	a00215
	уиа	0(М6)
pr202	мода	loc116(M13)
	уиа	0(М16)
A00203	мод	idx(М13)
	зп	loc20(М13)
	и	=В'1777777'
	зп	loc116(М13)
	счи	М16
	зп	usrloc
	слц	=В'2'
	зп	loc50(М13)
	сч	loc116(М13)
	нтж	loc246(М13)
	по	(М6)
	сч	loc116(М13)
	зп	loc246(М13)
	пб	a00530
********************
a00212	мода	cmd1a
	уиа	0(М6)
A00213	сч	=В'2000'
	мода	loc54(M13)
	уиа	0(М16)
	пб	A00203
a00215	мода	loc120(M13)
	уиа	0(М16)
	пб	A00164
********************
cmd54	сда	64+6
	зп	curcmd(М13)
	и	=В'77'
	уи	М16
	сч	loc12(М13)
	мода	(М16)
	мод	(M13)
	зп	
	пб	(М6)
********************
move	сда	64+6
	зп	curcmd(М13)
	и	=В'77'
	уи	М16
	сч	curcmd(М13)
	сда	64+6
	зп	curcmd(М13)
	и	=В'77'
	уи	М5
	мода	(М5)
	сч	bdvect(М13)
	пб	A00241
********************
cmd50	мода	next
	уиа	0(М6)
cmd51	уи	М16
	сч	curcmd(М13)
	сда	64+6
	зп	curcmd(М13)
	и	=В'77'
	уи	М5
	сли	М5(М13)
	сч	(М5)
	слц	=В'1'
	зп	(М5)
	мод	(М5)
	сч	-1
A00241	мода	(М16)
	зп	bdvect(М13)
	пб	(М6)
********************
cmd52	сда	64+6
	зп	curcmd(М13)
	и	=В'77'
	пб	cmd51
********************
cmd46	сда	64+18
	и	=В'77777'
	вч	loc53(М13)
	зп	loc42(М13)
	сч	myloc(М13)
	сда	64+33
	и	=В'17777'
	по	a00562
	зп	mylen(М13)
	пб	(М6)
********************
lock	сда	64-20
	или	e70msk(М13)
	зп	work
	или	=М39В'1'
	зп	ИСЭ70
	Э70	ИСЭ70
	мод	bdtab(M13)
	сч	1
	нтж	=М31В'1'
	мод	bdtab(M13)
	зп	1
	и	=М31В'1'
	по	clash
	мод	bdbuf(M13)
	зп	
	Э70	work
	пб	(М6)
********************
cmd44	зп	adescr(М13)
	пб	(М6)
********************
cmd36	мод	loc50(M13)
	уиа	0(М16)
	мод	idx(М13)
	мод	array(M13)
	зп	1(М16)
	пб	a01160
********************
a00267	пв	pr531,М5
a00270	сч	temp
	уи	М16
	вч	curpos(М13)
	пе	A00274
	зп	temp
	мод	curpos(M13)
	уиа	0(М16)
	уиа	0(М5)
A00274	мод	jmpoff
	пб	избазы
********************
избазы	пио	готово,М16
        переп	aitem(M13),usrloc
	пб	избазы
********************
вбазу	сч	which(М13)
	по	a00303
	сч	=В'1'
a00303	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пио	готово,М16
        переп	usrloc,aitem(M13)
	пб	вбазу
********************
сравн	мод	aitem(M13)
	сч	-1(М16)
	мод	usrloc
	нтж	-1(М16)
	пе	k37
	слиа	-1(М16)
	пино	сравн,М16
готово	сч	usrloc
	слц	curpos(М13)
	зп	usrloc
a00317	пио	a00325,М5
	сч	aitem(М13)
	слц	temp
	зп	aitem(М13)
	сч	curpos(М13)
	вч	temp
	зп	curpos(М13)
	мод	aitem(M13)
	сч	
	зп	loc41(М13)
	пб	(М6)
********************
a00325	сч	loc220(М13)
	сда	64+20
	и	=В'1777777'
	пе	a00267
	сч	jmpoff
	нтж	=a(готово-избазы)
	по	a00332
	сч	temp
	по	(М6)
a00332	уиа	7(М16)
	пб	skip
********************
cmd7	зп	jmpoff
	сч	loc42(М13)
A00334	зп	temp
	слц	loc53(М13)
	зп	loc53(М13)
	сч	myloc(М13)
	зп	usrloc
	пб	a00270
********************
cmd43	сч	mylen(М13)
	пб	A00334
********************
a00340	пв	pr531,М5
	пб	k07
********************
pr342	пв	pr531,М5
	мод	aitem(M13)
	сч	
	зп	loc41(М13)
	и	=В'77777'
	зп	itemln(М13)
	сч	
	зп	loc53(М13)
	пб	(М6)
********************
chkpsw	нтж	savedp(М13)
	по	cmd0
	уиа	17(М16)         ошибка НЕПР. ПАРОЛЬ
	пб	err
********************
ckdnex	нтж	key(М13)    сравнили текущий ключ (СМ) с искомым
	пе	cmd0   не совпал - пошли дальше
	уиа	9(М16) совпал - ошибка ИМ. УЖЕ ЕСТЬ
	пб	skip
********************
ckexst	нтж	key(М13)    сравнили текущий ключ (СМ) с искомым
	по	cmd0   совпал - пошли дальше
	уиа	8(М16) не совпал - ошибка НЕТ ИМЕНИ
	пб	skip
********************
a00355	слц	=В'2'
	уи	М5
	мод	loc50(M13)
	нтж	-1
	и	=В'1777'
	пе	A00367
	мод	loc50(M13)
	сч	-1
	и	=М10В'1777777'
	по	nonext
	сда	64+10
	пв	pr202,М6
	мод	idx(М13)
	сч	bdtab(М13)
	слц	=В'100000'
	мод	idx(М13)
	зп	bdtab(М13)
	уиа	0(М5)
A00367	счи	М5
	или	=М30В'1'
	мод	idx(М13)
	зп	array(М13)
a00371	мод	loc50(M13)
	сч	(М5)
	зп	curkey(М13)
	мод	loc50(M13)
	сч	1(М5)
	зп	adescr(М13)
	пб	a00145
cmd5	мода	flush
	уиа	0(М6)
pr376	сч	loc20(М13)
	зп	D00012
	сч	=В'41'
	зп	mylen(М13)
	сч	=В'2'
	зп	D00010
	мода	D00010
	уиа	0(М16)
	счи	М16
	зп	usrloc
	пб	(М6)
cmd6	сч	=В'41'
	зп	itemln(М13)
	мода	loc54(M13)
	уиа	0(М16)
	сч	adescr(М13)
	пб	A00203
a00407	счи	М6
	зп	goto
cmd4	мод	idx(М13)
	сч	array(М13)
	по	empty
	уи	М5
	пио	a00355,М16
	пио	a00415,М5
A00414	слиа	-2(М5)
	пб	A00367
a00415	мод	loc50(M13)
	сч	-1
	сда	64+29
	по	nopred
	пв	pr202,М6
	мод	idx(М13)
	сч	bdtab(М13)
	вч	=В'100000'
	мод	idx(М13)
	зп	bdtab(М13)
	сч	loc117(М13)
	и	=В'1777'
	уи	М5
	пб	A00414
pr425	мод	dblen(M13)
	уиа	0(М16)
	сч	
	зп	itemln(М13)
A00427	мод	loc34(M13)
	сч	-1(М16)
	по	a00431
	вч	=В'1'
a00431	слц	itemln(М13)
	зп	itemln(М13)
	слиа	-1(М16)
	пино	A00427,М16
	пб	(М6)
********************
fnendc	уи	М16
	сч	myloc(М13)
	слц	mylen(М13)
	зп	work2
	сч	loc40(М13)
	сда	64+8
	пе	a00440
	сч	loc40(М13)
a00440	сда	64-40
	зп	loc116(М13)
	уиа	-4(М5)
a00442	сда	64+8
	или	loc116(М13)
	мода	a00442
	цикл	(М5)
	зп	loc116(М13)
A00445	уиа	-5(М5)
	мод	work2
	сч	(М16)
	нтж	loc116(М13)
A00447	сда	64+40
	уи	М6
	пио	a00455,М6
	счмр	
A00452	мода	A00447
	цикл	(М5)
A00453	мода	A00445
	цикл	(М16)
	уиа	10(М16)         ошибка НЕТ С. КОНЦА
	пб	err
a00455	счмр	
	нтж	loc116(М13)
	зп	D00031
	сч	loc40(М13)
	сда	64+8
	по	a00470
	сч	loc40(М13)
	сда	64-40
	пио	a00465,М5
	нтж	D00031
	сда	64+40
	по	a00470
	сч	D00031
	нтж	loc116(М13)
	пб	A00452
a00465	мод	work2
	мода	1(М16)
	нтж	
	сда	64+40
	пе	A00453
	слиа	1(М16)
a00470	мод	mylen(М13)
	слиа	1(М16)
	счи	М16
	зп	mylen(М13)
	пб	cmd0
********************
fnendw	уи	М16
	сч	myloc(М13)
	слц	mylen(М13)
	зп	work2
	мод	work2
	сч	(М16)
	нтж	loc40(М13) 
	по	a00470  нашли признак конца
	мода	fnendw
	цикл	(М16)
A00500	уиа	11(М16) СИСТ. ОШИБКА
	пб	err
********************
a00501	сч	bdbuf(М13)
	пб	A00505
pr502	зп	which(М13)
	или	rootfl(М13)
	зп	D00015
	и	=В'1777'
	пе	a00501
	сч	bdtab(М13)
A00505	зп	loc241(М13)
	уи	М16
	сч	(М16)
	нтж	D00015
	по	(М7)
	сч	which(М13)
	по	a00520
	сч	dirty(М13)
	и	=В'2'
	по	a00520
	нтж	dirty(М13)
	зп	dirty(М13)
	сч	bdbuf(М13)
	сда	64-20
	или	e70msk(М13)
	зп	ИСЭ70
	сч	(М16)
	и	=В'1777'
	слц	ИСЭ70
	зп	ИСЭ70
	Э70	ИСЭ70
a00520	сч	loc241(М13)
	сда	64-20
	или	which(М13)
	или	=М39В'1'
	слц	e70msk(М13)
	зп	ИСЭ70
	Э70	ИСЭ70
	мод	loc241(M13)
	уиа	0(М16)
	сч	(М16)
	нтж	D00015
	по	(М7)
corupt	уиа	2(М16)          ошибка ЗАТЕРТ ЛИСТ
	пб	err
********************
a00530	мода	a01423          на продолжение копирования
	уиа	0(М5)
pr531	зп	D00040
	и	=В'1777'
	пв	pr502,М7
A00533	сч	D00040
	и	E20П11
	по	zerkey          ошибка НУЛ.УКЛЮЧ
	зп	work
	сда	64-29
	зп	work2
	сч	1(М16)
	и	E20П11
	по	delrec          ошибка НЕТ ЗАПИСИ
	вч	work
	по	a00555
	сл	work
A00541	сда	64+10
	зп	loc116(М13)
A00542	мод	loc116(M13)
	сч	1(М16)
	зп	loc220(М13)
	нтж	work2
	и	=М39В'777'
	по	a00550
	сч	loc116(М13)
	вч	=В'1'
	зп	loc116(М13)
	или	
	пе	A00542
	пб	delrec          ошибка НЕТ ЗАПИСИ
********************
a00550	сч	loc220(М13)
	и	=В'1777'
	слц	=В'1'
	слц	loc241(М13)
	зп	aitem(М13)
	сч	loc220(М13)
	и	E20П11
	сда	64+10
	зп	curpos(М13)
	пб	(М5)
********************
a00555	сч	work
	пб	A00541
E20П11	конд	м10в'1777'
********************
* условная ошибка - если команда была последней
* или если за ней не НОП
skip	сч	curcmd(М13)
	сда	64+6
	по	err
	и	=В'77'
	пе	err
* если не фиксировать ошибку, то пропустить 4 команды
a00562	сч	curcmd(М13)
	сда	64+30
	пб	next
********************
zerkey	уиа	1(М16)
	пб	err
delrec	уиа	3(М16)
	пб	err
badnam	уиа	4(М16)
	пб	err
corr1p	уиа	5(М16)
	пб	err
overfl	уиа	6(М16)
	пб	err
clash	уиа	13(М16)
	пб	err
empty	уиа	14(М16)
	пб	err
nopred	уиа	15(М16)
	пб	skip
nonext	уиа	16(М16)
	пб	skip
********************
root	зп	rootfl(М13)
	сч	arch
	зп	dbdesc(М13)
	пб	setctl
********************
cmd12	слц	=A(dbdesc)
	зп	myloc(М13)
	сч	=В'3'
	зп	mylen(М13)
	сч	adescr(М13)
	пб	cpyout
********************
setctl	и	=В'777777'
	зп	e70msk(М13)
	сч	dbdesc(М13)
	сда	64+18
	зп	dblen(М13)
	сч	bdtab(М13)
	сда	64-20
	слц	e70msk(М13)
	или	=М39В'1'
	зп	ИСЭ70
	Э70	ИСЭ70
	сч	bdtab(М13)
	зп	loc241(М13)
	уи	М16
	сч	(М16)
	нтж	rootfl(М13)
	пе	corr1p
	зп	idx(М13)
	зп	loc246(М13)
	зп	which(М13)
	сч	3(М16)
	и	=В'1777'
	слц	bdtab(М13)
	слц	=В'2'
	зп	loc34(М13)
	пб	A00213
********************
err	сч	erhndl(М13)
	по	a00622
	зп	savm16
a00622	уии	М7(М16)
	сли	М16(М16)
	мода	(М16)
	сч	msg-2
	зп	D00010
	мода	1(М16)
	сч	msg-2
a00626	зп	D00011
	сч	sync(М13)
	по	save
	нтж	=В'1'
	пе	exit1
	мод	bdtab(M13)
	уиа	0(М16)
	сч	1(М16)
	и	=М31В'1'
	по	save
	нтж	1(М16)
	зп	1(М16)
	пб	wrtab
save	сч	dirty(М13)
	и	=В'1'
	по	wrbuf
wrtab	сч	bdtab(М13)
	сда	64-20
	или	e70msk(М13)
	зп	ИСЭ70
	Э70	ИСЭ70
wrbuf	сч	dirty(М13)
	и	=В'2'
	по	exit
	сч	bdbuf(М13)
	сда	64-20
	или	e70msk(М13)
	зп	ИСЭ70
	мод	bdbuf(M13)
	сч	
	и	=В'1777'
	слц	ИСЭ70
	зп	ИСЭ70
	Э70	ИСЭ70
exit	сч	
	зп	dirty(М13)
exit1	рж	savrk
	счи	М7
	мода	D00010
	уиа	0(М16)
	мод	savm6
	уиа	0(М6)
	мод	savm7
	уиа	0(М7)
	мод	savm5
	уиа	0(М5)
	мод	savm4(M13)
	уиа	(M4)
	мод	3(М16)
	уиа	(M13)
	мод	-1(М16)
	пб	
********************
* запись и установка признака модификации (dirty(М13))
* в зависимости от which(М13)
pr662	мод	aitem(M13)
	зп	
	сч	which(М13)
	по	a00665
	сч	=В'1'
a00665	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пб	(М6)
********************
a00667	сч	idx(М13)
	по	a01160
	сч	loc246(М13)
	пв	free,М6
	сч	=В'1'
	зп	loc42(М13)
	сч	loc117(М13)
	зп	D00025
	и	=М29В'1777777'
	зп	D00032
	сда	64+29
	по	a00703
	пв	a00340,М6
	или	=М10В'1777777'
	нтж	=М10В'1777777'
	зп	loc117(М13)
	сч	D00025
	и	=М10В'1777777'
	или	loc117(М13)
	пв	pr662,М6
a00703	пв	pr1232,М5
cmd27	сч	loc50(М13)
	уи	М16
	мод	idx(М13)
	слц	array(М13)
	уи	М5
	нтж	loc50(М13)
	зп	loc116(М13)
	сч	-1(М16)
	и	=В'1777'
	зп	work2
	слц	loc50(М13)
	зп	work
A00712	сч	2(М5)
	зп	(М5)
	слиа	1(М5)
	счи	М5
	нтж	work
	пе	A00712
	сч	work2
	вч	=В'2'
	нтж	work2
	нтж	-1(М16)
	зп	-1(М16)
	и	=В'1777'
	по	a00667
	сч	loc116(М13)
A00721	и	=В'77777'
	пе	a01160
	сч	idx(М13)
	по	a01160
	сч	(М16)
	зп	D00011
	мода	a00730
	уиа	0(М5)
	счи	М5
	зп	goto
	слц	=В'1'
	зп	D00033
	пб	a01160
a00730	пб	a01242
a00731	сч	D00011
	мод	loc50(M13)
	уиа	0(М16)
	мод	idx(М13)
	мод	array(M13)
	зп	(М16)
	мод	idx(М13)
	сч	array(М13)
	пб	A00721
********************
free	пв	pr531,М5
A00737	сч	1(М16)
	сда	64+10
	и	=В'77777'
	зп	work2
	сч	loc116(М13)
	уи	М5
A00742	нтж	work2
	по	a00747
	мода	(М5)
	сч	2(М16)
	мода	(М5)
	зп	1(М16)
	слиа	1(М5)
	счи	М5
	пб	A00742
a00747	сч	loc220(М13)
	и	=В'1777'
	зп	D00031
a00751	слиа	-1(М5)
	пио	a00760,М5
	мода	(М5)
	сч	1(М16)
	и	=В'1777'
	вч	D00031
	по	a00751
	мода	(М5)
	сч	1(М16)
	слц	curpos(М13)
	мода	(М5)
	зп	1(М16)
	пб	a00751
a00760	сч	1(М16)
	и	=В'1777'
	зп	work
	нтж	D00031
	по	a00773
	сч	D00031
	вч	work
	пе	A00500
	уи	М5
	сч	work
	слц	loc241(М13)
	зп	work
	слц	curpos(М13)
	зп	D00031
A00767	мод	work
	сч	(М5)
	мод	D00031
	зп	(М5)
	слиа	-1(М5)
	пино	A00767,М5
a00773	сч	1(М16)
	вч	=В'2000'
	слц	curpos(М13)
	зп	1(М16)
	сч	loc34(М13)
	уи	М16
	мод	which(М13)
	сч	(М16)
	слц	curpos(М13)
	слц	=В'1'
	мод	which(М13)
	зп	(М16)
	сч	which(М13)
	по	a01003
	сч	=В'1'
a01003	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	сч	loc220(М13)
A01005	сда	64+20
	и	=В'1777777'
	пе	free
	сч	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пб	(М6)
********************
date	сч	loc14(М13)
	и	=М15В'777777'
	зп	work
	Э50	'114'
	сбр	=М20В'77777'   предпоследняя цифра года игнорируется
	или	work
	или	mylen(М13)
	зп	D00023
	пб	(М7)
********************
flush	зп	usrloc
	пв	pr1022,М6
	сч	D00024
	зп	loc12(М13)
	пб	cmd0
********************
pr1022	пв	date,М7
A01023	сч	
	уиа	0(М5)
	зп	D00030
	сч	loc34(М13)
	уи	М16
	мод	which(М13)
	сч	(М16)
	зп	work
	сч	mylen(М13)
	слц	=В'1'
	зп	mylen(М13)
	вч	work
	по	a01301
	сч	which(М13)
A01032	пв	pr502,М7
	уии	М7(М16)
	сч	1(М7)
	сда	64+10
	уи	М16
A01035	сда	64-39
	мод	loc241(M13)
	нтж	1(М16)
	и	=М39В'777'
	по	a01044
	мод	loc241(M13)
	сч	1(М16)
	мод	loc241(M13)
	зп	2(М16)
	слиа	-1(М16)
	счи	М16
	пино	A01035,М16
a01044	слиа	1(М16)
	счи	М16
	зп	loc116(М13)
	сда	64-39
	или	D00030
pr1047	зп	D00030
	сда	64+39
	сда	64-10
	или	which(М13)
	зп	D00024
	сч	mylen(М13)
	уи	М16
	вчоб	1(М7)
	слц	=В'2000'
	зп	1(М7)
	и	=В'1777'
	зп	work
	слц	loc241(М13)
	слц	=В'1'
	зп	work2
	пино	A01062,М5
	слиа	-1(М16)
	сч	work2
	слц	=В'1'
	зп	work2
A01062	счи	М16
	по	a01067
A01063	переп	usrloc,work2
	пино	A01063,М16
a01067	сч	mylen(М13)
	сда	64-10
	или	work
	или	D00030
	мод	loc116(M13)
	зп	1(М7)
	мод	loc34(M13)
	уиа	0(М16)
	мод	which(М13)
	сч	(М16)
	вч	mylen(М13)
	вч	=В'1'
	мод	which(М13)
	зп	(М16)
	пио	a01111,М5
	сч	which(М13)
	по	a01101
	сч	=В'1'
a01101	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	сч	D00024
	сда	64-20
	зп	D00030
	сч	D00034
	зп	mylen(М13)
A01105	слиа	-1(М5)
	пино	A01264,М5
	мода	overfl
	уиа	0(М6)
	сч	D00030
	пб	A01005
a01111	сч	D00023
	мод	work2
	зп	-1
	сч	which(М13)
	по	a01114
	сч	=В'2'
a01114	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пб	(М6)
a01116	сч	
	зп	loc20(М13)
	сч	loc12(М13)
	мода	overfl
	уиа	0(М6)
	пб	free
********************
cmd26	зп	D00011
	сч	loc12(М13)
	зп	adescr(М13)
	пб	A01140
********************
a01123	мод	idx(М13)
	сч	array(М13)
	сда	64+15
A01125	зп	D00012
	сда	64+15
	уи	М16
	счмр	
	мод	D00033
	по	
	сч	D00012
	слиа	-1(М16)
	пио	A01133,М16
	слц	=В'2'
A01133	вч	=В'1'
	зп	D00012
	пв	a00407,М6
	сч	D00012
	пб	A01125
a01136	сч	D00032
	сда	64+29
	или	=М47В'1'
A01140	зп	D00024
	сч	loc50(М13)
	уи	М16
	мод	idx(М13)
	слц	array(М13)
	слц	=В'2'
	уи	М5
	счи	М5
	зп	loc36(М13)
	сч	-1(М16)
	и	=В'1777'
	уи	М6
	сли	М6(М16)
A01147	счи	М6
	нтж	loc36(М13)
	по	a01154
	сч	-2(М6)
	зп	(М6)
	сч	-1(М6)
	зп	1(М6)
	слиа	-2(М6)
	пб	A01147
a01154	сч	D00011
	зп	(М5)
	сч	D00024
	зп	1(М5)
	сч	-1(М16)
	слц	=В'2'
	зп	-1(М16)
a01160	счи	М16
	вч	=В'1'
	зп	usrloc
	сч	idx(М13)
	пе	a01167
A01163	сч	=В'41'
	зп	mylen(М13)
	сч	-1(М16)
	нтж	=В'40'
	по	a01252
	сч	loc20(М13)
	пб	A01311
a01167	сч	-1(М16)
	и	=В'1777'
	нтж	=В'100'
	по	a01174
A01171	сч	-1(М16)
	и	=В'1777'
	слц	=В'1'
	зп	mylen(М13)
	сч	loc246(М13)
	пб	A01311
a01174	сч	idx(М13)
	сда	64-1
	слц	=В'41'
	зп	work
	сч	loc55(М13)
	нтж	=В'36'
	пе	A01201
	сч	work
	слц	=В'44'
	зп	work
A01201	пв	pr425,М6
	вч	work
	пе	a01116
	сч	loc157(М13)
	зп	D00025
	сч	loc246(М13)
	сда	64-29
	зп	loc157(М13)
	сч	loc117(М13)
	и	=М10В'1777777'
	или	loc157(М13)
	или	=В'40'
	зп	loc157(М13)
	сч	usrloc
	слц	=В'40'
	зп	usrloc
	сч	=В'41'
	зп	mylen(М13)
	пв	pr1022,М6
	сч	D00025
	зп	loc157(М13)
	сч	loc160(М13)
	зп	D00011
	сч	loc117(М13)
	зп	D00025
	сда	64+29
	сда	64-19
	или	D00024
	сда	64-10
	или	=В'40'
	зп	loc117(М13)
	и	=М10В'1777777'
	сда	64-19
	зп	D00032
	мода	loc120(M13)
	уиа	0(М16)
	мода	a01231
	уиа	0(М5)
	счи	М5
	зп	goto
	счи	М16
	вч	=В'1'
	зп	usrloc
	пб	A01171
a01231	мода	a01136
	уиа	0(М5)
pr1232	счи	М5
	зп	D00033
	сч	=В'1'
	зп	loc42(М13)
	сч	D00025
	сда	64+10
	и	=В'1777777'
	по	a01242
	пв	a00340,М6
	сда	64-19
	сда	64+19
	или	D00032
	пв	pr662,М6
a01242	мода	loc56(M13)
	уиа	0(М16)
	счи	М16
	зп	loc50(М13)
	сч	idx(М13)
	по	a00143
	вч	=В'2'
	зп	idx(М13)
	нтж	
	по	a01123
	мода	a01123
	уиа	0(М6)
	мод	idx(М13)
	сч	loc20(М13)
	пб	pr202
********************
a01252	пв	pr425,М6
	вч	=В'101'
	пе	a01116
	пв	pr1022,М6
	мода	loc56(M13)
	уиа	0(М16)
	сч	D00024
	или	=М47В'1'
	зп	1(М16)
	сч	=В'2'
	зп	-1(М16)
	пб	A01163
a01261	сч	usrloc
	слц	mylen(М13)
	вч	=В'1'
	зп	usrloc
	мод	dblen(M13)
	уиа	0(М5)
A01264	мод	loc34(M13)
	сч	-1(М5)
	вч	=В'2'
	пе	A01105
	слц	=В'1'
	зп	work
	слиа	-1(М5)
	вч	mylen(М13)
	пе	a01274
	сч	usrloc
	вч	mylen(М13)
	слц	=В'1'
	зп	usrloc
	счи	М5
	уиа	0(М5)
	пб	A01032
a01274	сч	mylen(М13)
	вч	work
	зп	D00034
	сч	work
	зп	mylen(М13)
	вчоб	usrloc
	зп	usrloc
	счи	М5
	слиа	1(М5)
	пб	A01032
********************
a01301	сч	mylen(М13)
	вч	=В'1775'
	по	a01261
	уиа	-1(М7)
a01303	слиа	1(М7)
	счи	М7
	нтж	dblen(М13)
	по	a01261
	сч	mylen(М13)
	мод	loc34(M13)
	вч	(М7)
	по	a01303
	счи	М7
	пб	A01032
cmd20	зп	usrloc
	сч	adescr(М13)
A01311	зп	D00012
	мода	a00143
	уиа	0(М6)
	пв	pr531,М5
	вч	=В'1'
	уи	М5
	нтж	mylen(М13)
	пе	a01336
	пио	A01323,М5
A01317	мод	usrloc
	сч	-1(М5)
	мод	aitem(M13)
	зп	(М5)
	слиа	-1(М5)
	пино	A01317,М5
A01323	уии	М5(М16)
	пв	date,М7
	мод	aitem(M13)
	зп	
	сч	which(М13)
	по	a01330
	сч	=В'1'
a01330	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	сч	loc220(М13)
	и	=М20В'1777777'
	по	a00145
	нтж	loc220(М13)
	мод	loc116(M13)
	зп	1(М5)
	сч	loc220(М13)
	пб	A01005
a01336	пв	date,М7
	мод	loc241(M13)
	уиа	0(М16)
	сч	1(М16)
	сда	64+10
	уи	М5
	слиа	1(М5)
	вчоб	1(М16)
	и	=В'1777'
	слц	curpos(М13)
	вч	=В'2'
	зп	work
	вч	mylen(М13)
	пе	a01361
A01346	счи	М6
	зп	D00010
	сч	loc220(М13)
	зп	D00026
	и	=В'1777'
	зп	loc220(М13)
	пв	a00747,М6
	сч	mylen(М13)
	слц	=В'1'
	зп	mylen(М13)
	уиа	0(М5)
	мод	loc241(M13)
	уиа	0(М7)
	сч	D00026
	и	=М39В'777'
	пв	pr1047,М6
	сч	D00026
	мод	D00010
	уиа	0(М6)
	пб	A01005
********************
a01361	пв	pr425,М6
A01362	мод	aitem(M13)
	сч	
	и	=В'77777'
	слц	itemln(М13)
	вч	mylen(М13)
	пе	overfl
	мод	loc241(M13)
	уиа	0(М16)
	сч	work
	зп	mylen(М13)
	пв	A01346,М6
	сч	usrloc
	слц	mylen(М13)
	зп	usrloc
	сч	D00023
	и	=В'77777'
	вч	mylen(М13)
	зп	mylen(М13)
	мод	usrloc
	сч	-1
	зп	D00023
	пв	A01023,М6
	сч	D00012
	пв	pr531,М5
	сч	D00024
	сда	64-20
	или	loc220(М13)
	мод	loc116(M13)
	зп	1(М16)
	сч	which(М13)
	по	a01404
	сч	=В'2'
a01404	слц	=В'1'
	или	dirty(М13)
	зп	dirty(М13)
	пб	a00145
********************
cmd34	сда	64-33
	рзб	=П'77777'
	или	=М40П' '
	зп	loc40(М13)
	сч	loc41(М13)
	рзб	=П'03 1  '
	или	=М40П' '
	зп	loc41(М13)
	сч	=М40В'172'М32В'33'     конец текста
	зп	loc42(М13)
	пб	cmd0
********************        
cpyout	пв	pr342,М6
A01415	мода	cmd0
	уиа	0(М6)
	сч	myloc(М13)
	зп	usrloc
	сч	mylen(М13)
	пе	chklen  нулевую длину проверять не нужно
fits	сч	aitem(М13)
	слц	=В'1'
	зп	aitem(М13)
	сч	curpos(М13)
	вч	=В'1'
	зп	curpos(М13)
a01423	уи	М16
	счи	М16
	по	a01431  нечего копировать
coloop	переп	aitem(M13),usrloc
	пино	coloop,М16
a01431	сч	usrloc
	слц	curpos(М13)
	зп	usrloc
	сч	loc220(М13)
	и	=М20В'1777777'
	по	(М6)
	сда	64+20
	пб	a00530
********************
chklen	вч	itemln(М13)
	по	fits
	уиа	12(М16)         ошибка ДЛ.ЗАП>ДЛИН
	пб	err
********************
mkctl	сч	dbdesc(М13)
	сда	64+18
	зп	dblen(М13)
	уи	М5
	сч	dbdesc(М13)
	и	=В'777777'      НУЗЗЗЗ
	зп	e70msk(М13)
	сч	bdtab(М13)
	уи	М6
	зп	loc241(М13)
	сда	64-20
	или	e70msk(М13)
	зп	work2
	сч	=В'1777'
	зп	1(М6)
A01447	слиа	-1(М5)
	сч	=В'1776'
	мод	bdbuf(M13)
	зп	(М5)
	счи	М5
	или	rootfl(М13)
	зп	(М6)
	счи	М5
	слц	work2
	зп	ИСЭ70
	Э70	ИСЭ70
	пино	A01447,М5
	сч	bdbuf(М13)
	зп	loc34(М13)
	зп	myloc(М13)
	сч	=В'2000'
	зп	loc20(М13)
	сч	
	зп	D00011
	пв	pr376,М6
A01463	пв	pr1022,М6
A01464	сч	dblen(М13)
	зп	mylen(М13)
	вчоб	=В'1731'
	мод	bdbuf(M13)
	зп	
	уиа	17(М5)
	пб	A00153
********************
msg	конд	п'НУЛ.УК'
	конд	п'ЛЮЧ  0'в'172'
	конд	п'ЗАТЕРТ'
	конд	п' ЛИСТ0'в'172'
	конд	п'НЕТ ЗА'
	конд	п'ПИСИ 0'в'172'
	конд	п'НЕДОП.'
	конд	п' ИМЯ.0'в'172'
	конд	п'ЗАТЕРТ'
	конд	п' 1 Л.0'в'172'
	конд	п'ПЕРЕПО'
	конд	п'ЛНЕНА0'в'172'
	конд	п'ШАГЗ. '
	конд	п'ВЕЛИК0'в'172'
	конд	п'НЕТ ИМ'
	конд	п'ЕНИ  0'в'172'
	конд	п'ИМ.УЖЕ'
	конд	п' ЕСТЬ0'в'172'
	конд	п'НЕТ С.'
	конд	п'КОНЦА0'в'172'
	конд	п'СИСТ.О'
	конд	п'ШИБКА0'в'172'
	конд	п'ДЛ.ЗАП'
	конд	п'>ДЛИН0'в'172'
	конд	п'ОДНОВР'
	конд	п'.РАБ.0'в'172'
	конд	п'НЕТ ТЕ'
	конд	п'КУЩЕЙ0'в'172'
	конд	п'НЕТ ПР'
	конд	п'ЕД.З.0'в'172'
	конд	п'НЕТ СЛ'
	конд	п'ЕД.З.0'в'172'
	конд	п'НЕПР.П'
	конд	п'АРОЛЬ0'в'172'
        литер
********************
 эквив (BDVECT,0),(OUTADR,1),(LOC3,3),(CURCMD,5),(SYNC,6)
 эквив (GIVENP,7),(KEY,'10'),(ERHNDL,'11'),(LOC12,'12')
 эквив (MYLOC,'13'),(LOC14,'14'),(MYLEN,'15'),(BDBUF,'16')
 эквив (BDTAB,'17'),(LOC20,'20'),(ARRAY,'21')
 эквив (DBDESC,'30'),(ROOTFL,'31'),(SAVEDP,'32')
 эквив (SAVM4,'33'),(LOC34,'34'),(ADESCR,'35')
 эквив (LOC36,'36'),(CURKEY,'37'),(LOC40,'40')
 эквив (LOC41,'41'),(LOC42,'42'),(E70MSK,'44')
 эквив (CURPOS,'45'),(AITEM,'46'),(ITEMLN,'47')
 эквив (LOC50,'50'),(DBLEN,'51'),(LOC53,'53')
 эквив (LOC54,'54'),(LOC55,'55'),(LOC56,'56')
 эквив (LOC116,'116'),(LOC117,'117'),(LOC120,'120')
 эквив (LOC157,'157'),(LOC160,'160'),(LOC220,'220')
 эквив (LOC241,'241'),(IDX,'242'),(WHICH,'244')
 эквив (LOC246,'246'),(DIRTY,'247')
	ФИНИШ
eoi$$$
trn$$$
0-0
тпс
наз    02000
зонгп 670000     1
end$$$
*end file
``````
ЕКОНЕЦ
