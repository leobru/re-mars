шифр 419900зс5^
dis 52(1234-wr)^
ОЗУ 32^
ацп 999^
еев1а3
*name 
*no list
*assem
          PASACD:,NAME,DTRAN  /01.06.84/
          *BDV*:,LC,1
          PASBDI:,SUBP,
          PASBD:,SUBP,
          ,ATI,14
          14,XTA,
          ,ITS,13
          15,ATX,
          13,VJM,PASBDI
          ,WTC,*BDV*
          14,VTM,
          15,XTA,-2
          ,ASN,64-6
          ,ASN,64+6
          14,ATX,30B
          ,UTC,*0015B.=10
          ,XTA,
          14,ATX,3
          ,UTC,*0014B.=4000
          ,XTA,
          14,ATX,31B
          13,VJM,PASBD
          15,UTM,-3
          15,WTC,2
          ,UJ,
          *0014B:,LOG,4000
          *0015B:,LOG,10
          ,END,
          PASSETAR:,NAME,DTRAN  /01.06.84/
          PASBDI:,SUBP,
          БАНДИТ:,SUBP,
          ,ASN,64-6
          ,ASN,64+6
          ,ITS,13
          15,ATX,
          13,VJM,PASBDI
          15,XTA,
          ,STI,13
          ,UTC,БАНДИТ
          ,ATX,4
          13,UJ,
          ,END,
          PASBDI:,NAME,DTRAN  /01.06.84/
          P/HT:,SUBP,
          *ERSTOP*:,LC,1
          *BDV*:,LC,1
          *BDC*:LC,BLOCK,CW70,OPEN,DEL,NEW,PUT,GET,MOD
          *BDVECT*:,LC,168
          *BDERRN*:,LC,1
          БАНДИТ:,SUBP,
          P/BDTAB:,PC,1024
          P/BDBUF:,PC,1024
          GAL/3:,EQU,БАНДИТ+3
          P/PRINT:,SUBP,
          PASBDE:,SUBP,
          GAM/2:,EQU,БАНДИТ+2
          PASBDI:,SUBP,
          9,VTM,БАНДИТ
          9,XTA,3
          13,U1A,
          10,VTM,*BDVECT*
          ,ITA,10
          9,ATX,3
          ,UTC,*BDV*
          ,ATX,
          15,MTJ,12
          ,XTA,
          10,MTJ,15
          14,VTM,77531B
          *0012B:15,ATX,
          14,VLM,*0012B
          12,MTJ,15
          14,VTM,P/BDTAB
          ,ITA,14
          10,ATX,17B
          14,VTM,P/BDBUF
          ,ITA,14
          10,ATX,16B
          14,VTM,ERRHANDL
          ,ITA,14
          10,ATX,11B
          13,UJ,
          ERRHANDL:,UTC,*BDERRN*
          ,ATX,
          ,ASN,64-42
          14,VTM,*0054B
          14,AUX,2
          14,ARX,3
          10,VTM,*0041B
          10,ATX,2
          ,ITA,13
          ,ASN,64-33
          14,AUX,
          14,ARX,1
          10,ATX,
          ,WTC,GAL/3
          ,XTA,5
          ,ASN,64-30
          14,AUX,4
          14,ARX,5
          10,ATX,6
          ,WTC,GAL/3
          ,XTA,10B
          11,VTM,*0053B
          11,ATX,
          ,ITA,13
          15,ATX,
          13,VJM,P/PRINT
          ,UTC,*ERSTOP*
          ,XTA,
          ,U1A,P/HT
          13,VJM,PASBDE
          15,WTC,
          ,UJ,
          *0041B:,BSS,1
          ,ISO,6HB ADR,
          ,BSS,1
          ,ISO,18H DATA BASE ERROR,
          ,BSS,1
          ,ISO,18H MICRO RUN, NAME=
          *0053B:,BSS,1
          *0054B:,ISO,6H′′′7′′7′′7′′7′′7′
          ,ISO,6H 00000
          ,ISO,6H′′′′′′′′′7′′7′
          ,ISO,6H    00
          ,ISO,6H′7′′7′′7′′7′′7′′7′
          ,ISO,6H000000
C===========================================
          PASBD:,ENTRY,
C===========================================
          *0062B:13,MTJ,14
          ,ITA,4
          4,VTM,БАНДИТ
          4,ATX,33B
          ,UTC,*BDV*
          ,XTA,
          ,UTC,БАНДИТ
          ,ATX,3
          ,UJ,GAM/2
C===========================================
          PAIB:,ENTRY,
C===========================================
          ,ITA,13
          ,ITS,14
          ,ITS,11
          15,ATX,
          13,VJM,PASBDI
          ,WTC,*BDV*
          9,VTM,
          15,XTA,-5
          ,UTC,*0117B.=3777 7777 7777 7777
          ,AAX,
          9,ATX,10B
          15,XTA,-4
          ,UTC,*0122B.=7 7777
          ,AAX,
          9,ATX,15B
          15,XTA,-1
          9,ATX,13B
          15,WTC,-2
          ,UTC,OPEN
          ,XTA,
          9,ATX,3
          ,U1A,*0114B
          ,ITA,15
          9,ATX,13B
          15,WTC,-1
          ,XTA,
          ,ASN,64-1
          ,ASN,64+1
          9,XTS,10B
          ,ASN,64-10
          ,UTC,*0116B.=2
          ,XTS,
          9,ATX,15B
          ,UTC,NEWD1
          ,XTA,
          9,ATX,3
          13,VJM,*0062B
          ,UTC,NEWD2
          ,XTA,
          9,ATX,3
          15,UTM,-2
          *0114B:15,WTC,-3
          13,VTM,
          15,UTM,-5
          ,UJ,*0062B
          *0116B:,LOG,2
          *0117B:,LOG,3777 7777 7777 7777
          NEWD1:,LOG,26 2115 1131
          NEWD2:,LOG,1012 1411
          *0122B:,LOG,7 7777
          ,DATA,
          /0123B:,LOG,10 0000 0040 0027
          OPEND:,LOG,25 1214 1131
          DELD:,LOG,2723 1411
          ,LOG,
          PUTD:,LOG,2621 1511
          GETD:,LOG,22 1411
          MODD:,LOG,20 4026 2100 1511
          7,SET,/0123B
          1,,CW70
          ,END,
*stand
*bemsh
inp$$$
*****************************************************
* АРХИВНАЯ СИСТЕМА МАРС-6:                          *
* РАБОТА С АССОЦИАТИВНЫМИ МАССИВАМИ КЛЮЧ-ДАННЫЕ     *
* ВО ВНЕШНЕЙ ПАМЯТИ (ДИЗАССЕМБЛИРОВАНИЕ, АНАЛИЗ)    *
* ИСХОДНО В. И. ФИЛИППОВ, 1978-80                   *
*****************************************************
       macro
&l     m16=  &a
&l     МОДА  &a.(m13)
       УИА   (M16)
       mend
       macro
&l     do    &op,&base,&idx
&l     МОД   &idx.(m13)
       &op   &base
*      mnote * &op &base [ &idx ]
       mend
       macro
&l     call  &dest,&reg
&l     МОДА  &dest
       ПВ    (&reg)
       mend
       macro
&l     link &dest,&reg
&l     МОДА  &dest
       УИА   (&reg)
       mend
       macro
&l     dirty &i
&l     СЧ    which(M13)
       ПО    drt&sndx
       СЧ    =В'&i'
drt&sndx  СЛЦ   =В'1'
       ИЛИ   DIRТУ(M13)
       ЗП    DIRТУ(M13)
       mend
       macro
&l     ++    &a
&l     сч    &a
       слц   =В'1'
       зп    &a
       mend
       macro
&l     --    &a
&l     сч    &a
       вч    =В'1'
       зп    &a
       mend
       macro
&l     cycle &a,&r
&l     слиа -1(&r)
       мода &a
       пино (&r)
       mend
       macro
&l     цикл  &dest,&reg
&l     МОДА  &dest
       vlm   (&reg)
       mend
        macro
&l      переп    &a,&b
&l      мод     &a
        сч      -1(М16)
        мод     &b
        зп      -1(М16)
        слиа    -1(М16)
        mend
        macro
&l      decl    &n
        gbla &off
        aif  (&l eq '').skip
&l      equ     &off
.skip   anop
&off    seta    &off+&n
        mend
&off    seta 0        
БАНДИТ СТАРТ ’2000’
М4     ЕQU   4 базовый регистр кода
М13    ЕQU   11 указатель на вектор данных
М5     ЕQU   5
М6     ЕQU   6
М7     ЕQU   7
М16    ЕQU   14
       УПОТР БАНДИТ(М4)
       КОНД  В’0’
       КОНД  П’БАНДИТ’
SТАRТ  СЧИ   М16 основная точка входа, вызов по М16
       ПБ    ЕNТЕR
АВDV   ПАМ   1
АRСН   КОНД  В’1550063’ dflt каталог архива НУ 53, зона 63
       СЧИ   М16  дополнительная точка входа?
       ЗП    SАVМ16
       ПБ    ЕNТЕR2
SАVМ16 ПАМ   1
D00010 КОНД  П’190280’  дата версии?
D00011 ПАМ   1
D00012 ПАМ   1
SАVМ13 ПАМ   1
GОТО   ПАМ   1
ключзн ПАМ   1
USRLОС ПАМ   1
SАVМ7  ПАМ   1
SАVМ6  ПАМ   1
SАVМ5  ПАМ   1
JМРОFF ПАМ   1
datlen ПАМ   1
D00024 ПАМ   1
D00025 ПАМ   1
D00026 ПАМ   1
negkey ПАМ   1
D00030 ПАМ   1
D00031 ПАМ   1
D00032 ПАМ   1
SАVМ4  ПАМ   1
remlen ПАМ   1
WОRК   ПАМ   1
temp   ПАМ   1
ИСЭ70  ПАМ   1
D00040 ПАМ   1
SАVRК  ПАМ   1
* Все команды приходят на дешифровщик с 0 на См.
SWIТСН ПБ    СМD0
       ЗП
* Команда 01: установка текущей записи на начало
К01    ЗП    negkey := 0
       ПБ    СМD1
* Команда 02: установка текущей записи на конец
К02    СЧ    =М47В'1'В'-1' (всеед без 48 р)
       ПБ    FIND
* Команда 03: шаг назад
К03    СЧ    =В'1'
       ЗП
* Команда 04: шаг вперед
К04    УИ    М16
       ПБ    СМD4
К05    ЗП    D00011
       ПБ    СМD5
К06    ЗП    idx(M13)
       ПБ    СМD6
К07    СЧ    =a(a00317-избазы)
       ПБ    СМD7
* Команда 10: создать базу/каталог; сама потом выполняет 21
К10    ЗП    which(M13)
       ПБ    МКСТL
* Команда 11: поиск элемента по ключу
К11    СЧ    key(M13)
       ПБ    FIND
* Команда 12: скопировать текущий элемент в дескриптор базы
К12    СЧИ   М13
       ПБ    СМD12
К13    ЗП    itmlen(M13)
       ПБ    РR425
* Команда 14: проверка совпадения ключей
* Если команда последняя или за ней не 00,
* то неудача - ошибка,
* иначе пропустить 00 и следующие 3 команды
К14    СЧ    curkey(M13)
       ПБ    СКЕХSТ
* Команда 15: проверка несовпадения ключей; аналогично 14
К15    СЧ    curkey(M13)
       ПБ    СКDNЕХ
* Команда 16: поиск символа конца строки
К16    ВЧ    mylen(M13)
       ПБ    СМD16
* Команда 17: поиск слова-признака конца
К17    ВЧ    mylen(M13)
       ПБ    СМD17
* Команда 20: копирование из памяти в текущий элемент
К20    СЧ    myloc(M13)
       ПБ    СМD20
* Команда 21: сохранение буфера на диск
К21    СЧ    myloc(M13)
       ПБ    FLUSН
* Команда 22: копирование текущего элемента в память
* пользователя
К22    СЧ    adescr(M13)
       ПБ    СРУОUТ
* Команда 23: освобождение памяти элемента
К23    СЧ    adescr(M13)
       ПБ    FRЕЕ
* Команда 24: проверка 'пароля'
К24    СЧ    givenp(M13)
       ПБ    СНКРSW
* Команда 25: установить текущую базу по дескриптору
К25    СЧ    dbdesc(M13)
       ПБ    SЕТСТL
К26    СЧ    key(M13)
       ПБ    СМD26
К27    ЗП    limit(M13)
       ПБ    СМD27
* Команда 30: выполнить командное слово повторно
К30    СЧ    ORGCMD(M13)
       ПБ    NЕХТ
* Команда 31: установить корневой каталог в качестве текущего
К31    СЧ    =В'4000'
       ПБ    RООТ
К32    СЧ    loc12(M13)
       ПБ    СМD32
К33    СЧ    adescr(M13)
       ПБ    РR342
К34    СЧ    desc1(M13)
       ПБ    СМD34
* Команда 35: запись буферов на диск
К35    СЧ    dirty(M13)
       ПБ    SАVЕ
К36    СЧ    loc12(M13)
       ПБ    СМD36
* Команда 37: пропустить следующую команду
К37    СЧ    СURСМD(M13)
       СДА   64+12
* Команда 40: останов, нормальное завершение
К40    ПБ    NЕХТ
К41    СЧ    =a(сравн-избазы)
       ПБ    К43
К42    СЧ    =a(вбазу-избазы)
К43    ЗП    JМРОFF
       ПБ    СМD43
* Команда 44: adescr := loc41(М13)
К44    СЧ    desc1(M13)
       ПБ    СМD44
* Команда 45: установить флаг работы с базой
К45    СЧ    bdtab(M13)
       ПБ    LОСК
К46    СЧ    myloc(M13)
       ПБ    СМD46
* Команда 47: уход на первую ячейку bdvect
К47    СЧ    desc1(M13)
       ПБ    outadr(M13)(M13)
* Команда 50 хх: cmd := bdvect[bdvect[xx]++]
К50    СЧ    =В'3'
       ПБ    СМD50
* Команда 51 хх: myloc := bdvect[bdvect[xx]++]
К51    СЧ    =A(MYLOC)
       ПБ    СМD51
* Команда 52 xx yy: bdvect[xx] := bdvect[bdvect[yy]++]
К52    СЧ    СURСМD(M13)
       ПБ    СМD52
* Команда 53 xx yy: bdvect[xx] := bdvect[yy]
К53    СЧ    СURСМD(M13)
       ПБ    МОVЕ
* Команда 54 xx: bdvect[xx] := bdvect[012]
К54    СЧ    СURСМD(M13)
       ПБ    СМD54
* Команда 55: немедленный возврат
К55    МОД   SАVМ16
       ПБ
********************
* команда 32: *bdvec[38] = bdvec[10]
* и установка флага dirty
СМD32  do    зп,,aitem
       dirty 2
       ПБ    (М6)
********************
ЕNТЕR  ЗП    SАVМ16
       СЧИ   М13
       ЗП    SАVМ13
       СЧРЖ  ’7777’
       ЗП    SАVRК
       РЖА   ’3’
       СЧ    АВDV
       УИ    М13
       СЧИ   М7
       ЗП    SАVМ7
       СЧИ   М5
       ЗП    SАVМ5
       do    сч,,bdtab
       НТЖ   rootfl(M13)
       ПО    А00141 уже видим что хотим
       СЧ    e70msk(M13)
       ПО    А00141 нет тек. массива, нечего читать
       СЧ    bdtab(M13)
       СДА   64-20
       ИЛИ   =М39В'1'
       ИЛИ   e70msk(M13)
       ЗП    ИСЭ70
       Э70   ИСЭ70 считали таблицу массива
А00141 СЧИ   М6
       ЗП    SАVМ6
ЕNТЕR2 СЧ    ORGCMD(M13)
       ПБ    NЕХТ
*******************************
drtnxt СЧ    =В'1'
       ИЛИ   dirty(M13)
       ЗП    dirty(M13)
* если GOTO не 0, переходим по содержимому и обнуляем его
RTNEXT МОД   GОТО
       УИА   (М5)
       СЧ
       ЗП    GОТО
       МОДА  (М5)
       ПИНО  (М5)
* выборка следующей команды
СМD0   СЧ    СURСМD(M13)
       СДА   64+6
NЕХТ   ПО    err2 команды кончились
       ЗП    СURСМD(M13)
       И     =В'77'
       УИ    М5
А00153 link  СМD0,m6  возврат на выборку след. команды
       СЧ
       ЗП    GОТО
       МОДА  (М5)
       ПБ    SWIТСН
*******************************
* команда 11: поиск ключа в текущем массиве       
FIND   ЗП    negkey
       ПО    ВАDNАМ ключ не должен быть нулевым
       И     =М47В'1'
       ПЕ    ВАDNАМ в ключе не должно быть 48 р.
*      IDX := 0
СМD1   ЗП    idx(M13) 
       СЧ    negkey
       НТЖ   =М47В'1'В'-1'
       ЗП    negkey теперь в NEGKEY инверсный ключ
СМD1А  m16=  loc56(M13)
       СЧ    loc20(M13)
       ПО    А00212
А00164 СЧ    -1(М16)
       И     =В'1777'
       УИ    М5
       ПО    А00172
А00166 МОДА  (М5)
       СЧ    -2(М16)
       СЛЦ   negkey
       ПЕ    А00172
       СЛИА  -2(М5)
       МОДА  А00166
       ПИНО  (М5)
А00172 СЛИА  -2(М5)
       СЧИ   М5
       ИЛИ   =М30В'1'
       do    зп,array(M13),idx
       СЧ    1(М16)
       И     =М47В'1'
       ПО    А00371
       СЧ    idx(M13)
       СЛЦ   =В'2'
       ЗП    idx(M13)
       МОДА  (М16)
       СЧ    1(М5)
       link  А00215,m6
* процедура  возвращается по М6
* установка текущей записи по дескриптору на СМ
РR202  m16=  loc116(M13)
А00203 do    зп,loc20(M13),idx
       И     =В'1777777'
       ЗП    loc116(M13)
       СЧИ   М16
       ЗП    USRLОС
       СЛЦ   =В'2'
       ЗП    loc50(M13)
       СЧ    loc116(M13)
       НТЖ   loc246(M13)
       ПО    (М6)
       СЧ    loc116(M13)
       ЗП    loc246(M13)
       ПБ    А00530
*******************************
А00212 link  СМD1А,m6
А00213 СЧ    =В'2000'
       m16=  loc54(M13)
       ПБ    А00203
*******************************
А00215 m16=  loc120(M13)
       ПБ    А00164
*******************************
* команда 54: mem[bdvec[next_insn]] := bdvec[10] уточнить       
СМD54  СДА   64+6
       ЗП    СURСМD(M13)
       И     =В'77'
       УИ    М16
       СЧ    loc12(M13)
       МОДА  (М16)
       МОД   (М13)
       ЗП
       ПБ    (М6)
*******************************
* команда 53: bdvec[next_insn] := bdvec[next_next_insn]
МОVЕ   СДА   64+6
       ЗП    СURСМD(M13)
       И     =В'77'
       УИ    М16
       СЧ    СURСМD(M13)
       СДА   64+6
       ЗП    СURСМD(M13)
       И     =В'77'
       УИ    М5
       МОДА  (М5)
       СЧ    ВDVЕСТ(M13)
       ПБ    stbdv
*******************************
* команда 50: curcmd := bdvec[3] := *bdvec[next_insn]++
СМD50  link  NЕХТ,m6
* команда 51: bdvec[11] := *bdvec[next_insn]++
СМD51  УИ    М16
       СЧ    СURСМD(M13)
       СДА   64+6
       ЗП    СURСМD(M13)
       И     =В'77'
       УИ    М5
       СЛИ   М5(М13)
       ++    (М5)
       МОД   (М5)
       СЧ    -1
stbdv  МОДА  (М16)
       ЗП    ВDVЕСТ(M13)
       ПБ    (М6)
*******************************
* команда 52: bdvec[next_insn] := *bdvec[next_next_insn]++
СМD52  СДА   64+6
       ЗП    СURСМD(M13)
       И     =В'77'
       ПБ    СМD51
*******************************
СМD46  СДА   64+18
       И     =В'77777'
       ВЧ    loc53(M13)
       ЗП    desc2(M13)
       СЧ    myloc(M13)
       СДА   64+33
       И     =В'17777'
       ПО    skip5
       ЗП    mylen(M13)
       ПБ    (М6)
*******************************
* команда 45: захват базы
LОСК   СДА   64-20
       ИЛИ   e70msk(M13)
       ЗП    WОRК
       ИЛИ   =М39В'1'
       ЗП    ИСЭ70
       Э70   ИСЭ70
       do    сч,1,bdtab
       НТЖ   =М31В'1'
       do    зп,1,bdtab
       И     =М31В'1'
       ПО    СLАSН
       do    зп,,bdbuf
       Э70   WОRК
       ПБ    (М6)
*******************************
СМD44  ЗП    adescr(M13)
       ПБ    (М6)
*******************************
СМD36  do    уиа,(М16),loc50
       МОД   idx(M13)
       МОД   array(M13)
* *(loc50 + array[idx] + 1) := acc
       ЗП    1(М16)
       ПБ    А01160
*******************************
А00267 call  totlen,m5
А00270 СЧ    negkey
       УИ    М16
       ВЧ    curpos(M13)
       ПЕ    А00274
       ЗП    negkey
       do    уиа,(М16),curpos
       УИА   0(М5)
А00274 МОД   JМРОFF
       ПБ    ИЗБАЗЫ
*******************************
* перепись блока [aitem] -> [usrloc]
ИЗБАЗЫ МОДА  ГОТОВО
       ПИО   (М16)
       переп aitem(m13),usrloc
       ПБ    ИЗБАЗЫ
*******************************
* перепись блока [usrloc] -> [aitem]
* зачем-то признак dirty ставится в цикле
ВБАЗУ dirty 1
       МОДА  ГОТОВО
       ПИО   (М16)
       переп usrloc,aitem(m13)
       ПБ    ВБАЗУ
*******************************
СРАВН  do    СЧ,-1(М16),aitem
       МОД   USRLОС
       НТЖ   -1(М16)
       ПЕ    К37
       cycle СРАВН,m16
ГОТОВО СЧ    USRLОС
       СЛЦ   curpos(M13)
       ЗП    USRLОС
А00317 МОДА  А00325
       ПИО   (М5)
       СЧ    aitem(M13)
       СЛЦ   negkey
       ЗП    aitem(M13)
       СЧ    curpos(M13)
       ВЧ    negkey
       ЗП    curpos(M13)
       do    СЧ,,aitem
       ЗП    desc1(M13)
       ПБ    (М6)
*******************************
А00325 СЧ    loc220(M13)
       СДА   64+20
       И     =В'1777777'
       ПЕ    А00267
       СЧ    JМРОFF
       НТЖ   =А(ГОТОВО-ИЗБАЗЫ)
       ПО    А00332 не бывает 
       СЧ    negkey
       ПО    (М6)
А00332 УИА   7(М16) шагз. велик уточнить
       ПБ    SКIР
*******************************
СМD7   ЗП    JМРОFF
       СЧ    desc2(M13)
А00334 ЗП    negkey
       СЛЦ   loc53(M13)
       ЗП    loc53(M13)
       СЧ    myloc(M13)
       ЗП    USRLОС
       ПБ    А00270
*******************************
СМD43  СЧ    mylen(M13)
       ПБ    А00334
*******************************
А00340 call  totlen,m5
       ПБ    К07
*******************************
* процедура возвращается по М6       
РR342  call  totlen,m5
       do    СЧ,,aitem
       ЗП    desc1(M13)
       И     =В'77777'
       ЗП    itmlen(M13)
       СЧ
       ЗП    loc53(M13)
       ПБ    (М6)
*******************************
* команда 24: 'проверка пароля'
СНКРSW НТЖ   savedp(M13)
       ПО    СМD0
       УИА   17(М16) неверный пароль
       ПБ    ЕRR
*******************************
* команда 15: проверка, что ключа нет       
СКDNЕХ НТЖ   key(M13)
       ПЕ    СМD0
       УИА   9(М16) ключ уже есть
       ПБ    SКIР
*******************************
* команда 14: проверка, что ключ есть   
СКЕХSТ НТЖ   key(M13)
       ПО    СМD0
       УИА   8(М16) ключа нет
       ПБ    SКIР
*******************************
А00355 СЛЦ   =В'2'
       УИ    М5
       do    НТЖ,-1,loc50
       И     =В'1777'
       ПЕ    А00367
       do    СЧ,-1,loc50
       И     =М10В'1777777'
       ПО    NОNЕХТ нет след. записи
       СДА   64+10
       call  РR202,m6
       do    СЧ,bdtab(M13),idx
       СЛЦ   =М15В'1'
       do    ЗП,bdtab(M13),idx
       УИА   0(М5)
А00367 СЧИ   М5
       ИЛИ   =М30В'1'
       do    ЗП,array(M13),idx
А00371 do    СЧ,(М5),loc50
       ЗП    curkey(M13)
       do    СЧ,1(М5),loc50
       ЗП    adescr(M13)
       ПБ    RTNEXT
*******************************
* команда 5
СМD5   link  FLUSН,m6
* подготовка к формированию метаданных листа       
РR376  СЧ    loc20(M13)
       ЗП    D00012
       СЧ    =В'41' зачем писать столько слов - загадка
       ЗП    mylen(M13)
       СЧ    =В'2'
       ЗП    D00010
       link  D00010,m16
       СЧИ   М16
       ЗП    USRLОС
       ПБ    (М6)
*******************************
* команда 6
СМD6   СЧ    =В'41'
       ЗП    itmlen(M13)
       m16=  loc54(M13)
       СЧ    adescr(M13)
       ПБ    А00203
*******************************
* выполняем команду 4 как процедуру по М6
STEP   СЧИ   М6
       ЗП    GОТО
* команда 4: шаг вперед или назад
СМD4   do    СЧ,array(M13),idx
       ПО    ЕМРТУ нет тек. записи
       УИ    М5
       МОДА  А00355
       ПИО   (М16) шаг вперед
       МОДА  А00415
       ПИО   (М5)
А00414 СЛИА  -2(М5)
       ПБ    А00367
*******************************
А00415 do    СЧ,-1,loc50
       СДА   64+29
       ПО    NОРRЕD
       call  РR202,m6
       do    СЧ,bdtab(M13),idx
       ВЧ    =М15В'1'
       do    ЗП,bdtab(M13),idx
       СЧ    loc117(M13)
       И     =В'1777'
       УИ    М5
       ПБ    А00414
*******************************
* процедура возвращается по М6       
РR425  do    УИА,(М16),dblen
       СЧ
       ЗП    itmlen(M13)
А00427 do    СЧ,-1(М16),frebas
       ПО    А00431
       ВЧ    =В'1'
А00431 СЛЦ   itmlen(M13)
       ЗП    itmlen(M13)
       cycle А00427,m16
       ПБ    (М6)
********************
* команда 16: поиск символа конца
СМD16  УИ    М16    минус MYLEN
       СЧ    myloc(M13)
       СЛЦ   mylen(M13)
       ЗП    temp   myloc + mylen                
       СЧ    endmrk(M13)
       СДА   64+8
       ПЕ    А00440 не один байт
       СЧ    endmrk(M13)
А00440 СДА   64-40
       ЗП    loc116(M13) искомый байт в 48-41 рр
       УИА   -4(М5)
размн  СДА   64+8
       ИЛИ   loc116(M13) 
       ЦИКЛ  размн,М5
       ЗП    loc116(M13) размножили байт 6 раз    
цслово УИА   -5(М5)
       МОД   temp
       СЧ    (М16)
       НТЖ   loc116(M13)
цбайт  СДА   64+40
       УИ    М6
       МОДА  А00455
       ПИО   (М6)   нашли совпадение
       СЧМР
А00452 ЦИКЛ  цбайт,М5  по байтам
А00453 ЦИКЛ  цслово,М16 по словам
       УИА   10(М16) нет симв. конца
       ПБ    ЕRR
*******************************
А00455 СЧМР
       НТЖ   loc116(M13)     восстановили хвост слова
       ЗП    D00031     сохранили
       СЧ    endmrk(M13)
       СДА   64+8
       ПО    А00470     второго байта не было
       СЧ    endmrk(M13)
       СДА   64-40
       МОДА  А00465
       ПИО   (М5)       надо перейти через слово
       НТЖ   D00031
       СДА   64+40
       ПО    А00470     все совпало
       СЧ    D00031
       НТЖ   loc116(M13)     вернули хвост обратно
       ПБ    А00452     продолжаем поиск
*******************************
А00465 МОД   temp
       МОДА  1(М16)
       НТЖ
       СДА   64+40
       ПЕ    А00453     не вышло, продолжаем
       СЛИА  1(М16)
А00470 do    СЛИА,1(М16),mylen
       СЧИ   М16
       ЗП    mylen(M13)      настоящая длина
       ПБ    СМD0
*******************************
* команда 17: поиск слова конца       
СМD17  УИ    М16
       СЧ    myloc(M13)
       СЛЦ   mylen(M13)
       ЗП    temp
       МОД   temp
       СЧ    (М16)
       НТЖ   endmrk(M13)
       ПО    А00470
       ЦИКЛ  СМD17,М16
А00500 УИА   11(М16) сист. ошибка
       ПБ    ЕRR
********************
gzne0  СЧ    bdbuf(M13) буфер ненулевой зоны
       ПБ    gz1
*******************************
* считывание зоны с номером на СМ в соотв. буфер
* процедура возвращается по М7
getzon ЗП    which(M13)
       ИЛИ   rootfl(M13)
       ЗП    ключзн ключ зоны
       И     =В'1777'
       ПЕ    gzne0 номер зоны не 0
       СЧ    bdtab(M13) буфер 0-й зоны
gz1    ЗП    curbuf(M13)
       УИ    М16
       СЧ    (М16)
       НТЖ   ключзн
       ПО    (М7) зона уже какая надо
       СЧ    which(M13)
       ПО    gz2 хотим нулевую, bdbuf неинтересен
       СЧ    dirty(M13)
       И     =В'2'
       ПО    gz2 сохранение bdbuf не требуется
       НТЖ   dirty(M13)
       ЗП    dirty(M13)
       СЧ    bdbuf(M13)
       СДА   64-20
       ИЛИ   e70msk(M13)
       ЗП    ИСЭ70
       СЧ    (М16)
       И     =В'1777'
       СЛЦ   ИСЭ70
       ЗП    ИСЭ70
       Э70   ИСЭ70 сохранили старую зону
gz2    СЧ    curbuf(M13)
       СДА   64-20
       ИЛИ   which(M13)
       ИЛИ   =М39В'1'
       СЛЦ   e70msk(M13)
       ЗП    ИСЭ70
       Э70   ИСЭ70 считали новую зону
       do    УИА,(М16),curbuf
       СЧ    (М16)
       НТЖ   ключзн
       ПО    (М7) считали зону с ожидаемым ключом
corupt УИА   2(М16) затерт лист
       ПБ    ЕRR
********************
А00530 link  А01423,m5
* нахождение общей длины элемента
* процедура возвращается по М5       
totlen ЗП    D00040
       И     =В'1777'
       call  getzon,m7
А00533 СЧ    D00040
       И     Е20П11
       ПО    ZЕRkey
       ЗП    WОRК
       СДА   64-29
       ЗП    temp
       СЧ    1(М16)
       И     Е20П11
       ПО    DЕLRЕС
       ВЧ    WОRК
       ПО    А00555
       СЛ    WОRК
А00541 СДА   64+10
       ЗП    loc116(M13)
А00542 do    СЧ,1(М16),loc116
       ЗП    loc220(M13)
       НТЖ   temp
       И     =М39В'777'
       ПО    А00550
       --    loc116(M13)
       ИЛИ
       ПЕ    А00542
       ПБ    DЕLRЕС
*******************************
А00550 СЧ    loc220(M13)
       И     =В'1777'
       СЛЦ   =В'1'
       СЛЦ   curbuf(M13)
       ЗП    aitem(M13)
       СЧ    loc220(M13)
       И     Е20П11
       СДА   64+10
       ЗП    curpos(M13)
       ПБ    (М5)
*******************************
А00555 СЧ    WОRК
       ПБ    А00541
Е20П11 КОНД  В’0000000003776000’
********************
SКIР   СЧ    СURСМD(M13)
       СДА   64+6
       ПО    ЕRR
       И     =В'77'
       ПЕ    ЕRR
skip5  СЧ    СURСМD(M13)
       СДА   64+30
       ПБ    NЕХТ
*******************************
ZЕRkey УИА   1(М16)
       ПБ    ЕRR
DЕLRЕС УИА   3(М16)
       ПБ    ЕRR
ВАDNАМ УИА   4(М16)
       ПБ    ЕRR
СОRR1Р УИА   5(М16)
       ПБ    ЕRR
ОVЕRFL УИА   6(М16)
       ПБ    ЕRR
СLАSН  УИА   13(М16)
       ПБ    ЕRR
ЕМРТУ  УИА   14(М16)
       ПБ    ЕRR
NОРRЕD УИА   15(М16)
       ПБ    SКIР
NОNЕХТ УИА   16(М16)
       ПБ    SКIР
* команда 31: переключение на корневой каталог       
RООТ   ЗП    rootfl(M13)
       СЧ    АRСН
       ЗП    dbdesc(M13)
       ПБ    SЕТСТL
*******************************
* команда 12: копирование текущего элемента
* в дескриптор массива       
СМD12  СЛЦ   =A(DBDESC)
       ЗП    myloc(M13)
       СЧ    =В'3'
       ЗП    mylen(M13)
       СЧ    adescr(M13)
       ПБ    СРУОUТ
*******************************
* команда 25: переключение на массив, указанный в DBDESC       
SЕТСТL И     =В'777777'
       ЗП    e70msk(M13)
       СЧ    dbdesc(M13)
       СДА   64+18
       ЗП    dblen(M13)
       СЧ    bdtab(M13)
       СДА   64-20
       СЛЦ   e70msk(M13)
       ИЛИ   =М39В'1'
       ЗП    ИСЭ70
       Э70   ИСЭ70
       СЧ    bdtab(M13)
       ЗП    curbuf(M13)
       УИ    М16
       СЧ    (М16)  в нулевом слове массива хотим видеть
       НТЖ   rootfl(M13) ровно то же, что было в каталоге
       ПЕ    СОRR1Р иначе 'испорчен 1-й лист'
       ЗП    idx(M13) := 0
       ЗП    loc246(M13) := 0
       ЗП    which(M13) := 0
       СЧ    3(М16)
       И     =В'1777'
       СЛЦ   bdtab(M13)
       СЛЦ   =В'2'
       ЗП    frebas(M13)
       ПБ    А00213
*******************************
ЕRR    СЧ    erhndl(M13)
       ПО    err1 если установлен обработчик ошибок
       ЗП    SАVМ16 потом вернемся на него
err1   УИИ   М7(М16)
       СЛИ   М16(М16)
       МОДА  (М16)
       СЧ    МSG-2
       ЗП    D00010  первое слово текста ошибки
       МОДА  1(М16)
       СЧ    МSG-2
* сюда приходим с СМ=0, когда кончились команды
err2   ЗП    D00011 второе слово текста ошибки
       СЧ    LOC6(M13)
       ПО    SАVЕ
       НТЖ   =В'1'
       ПЕ    ЕХIТ1
       do    УИА,(М16),bdtab
       СЧ    1(М16)
       И     =М31В'1'
       ПО    SАVЕ не было признака занятости
       НТЖ   1(М16)
       ЗП    1(М16) сбрасываем его
       ПБ    WRТАВ нужно записать таблицу
*******************************
SАVЕ   СЧ    dirty(M13)
       И     =В'1'
       ПО    WRВUF 
WRТАВ  СЧ    bdtab(M13)
       СДА   64-20
       ИЛИ   e70msk(M13)
       ЗП    ИСЭ70
       Э70   ИСЭ70
WRВUF  СЧ    dirty(M13)
       И     =В'2'
       ПО    ЕХIТ
       СЧ    bdbuf(M13)
       СДА   64-20
       ИЛИ   e70msk(M13)
       ЗП    ИСЭ70
       do    СЧ,,bdbuf
       И     =В'1777'
       СЛЦ   ИСЭ70
       ЗП    ИСЭ70
       Э70   ИСЭ70
ЕХIТ   СЧ , выход с очисткой признака модификации
       ЗП    dirty(M13)
ЕХIТ1  РЖ    SАVRК
       СЧИ   М7
       link  D00010,m16
       МОД   SАVМ6
       УИА   (М6)
       МОД   SАVМ7
       УИА   (М7)
       МОД   SАVМ5
       УИА   (М5)
       do    УИА,(М4),loc33
       МОД   3(М16)
       УИА   (М13)
       МОД   -1(М16)
       ПБ , возврат в вызвавшую систему
*******************************
* процедура возвращается по М6       
РR662  do    ЗП,,aitem
       dirty 1
       ПБ    (М6)
***************************       
А00667 СЧ    idx(M13)
       ПО    А01160
       СЧ    loc246(M13)
       call  FRЕЕ,m6
       СЧ    =В'1'
       ЗП    desc2(M13)
       СЧ    loc117(M13)
       ЗП    D00025
       И     =М29В'1777777'
       ЗП    D00032
       СДА   64+29
       ПО    А00703
       call  А00340,m6
       ИЛИ   =М10В'1777777'
       НТЖ   =М10В'1777777'
       ЗП    loc117(M13)
       СЧ    D00025
       И     =М10В'1777777'
       ИЛИ   loc117(M13)
       call  РR662,m6
А00703 call  РR1232,m5
СМD27  СЧ    loc50(M13)
       УИ    М16
       do    СЛЦ,array(M13),idx
       УИ    М5
       НТЖ   loc50(M13)
       ЗП    loc116(M13)
       СЧ    -1(М16)
       И     =В'1777'
       ЗП    temp
       СЛЦ   loc50(M13)
       ЗП    WОRК
А00712 СЧ    2(М5)
       ЗП    (М5)
       СЛИА  1(М5)
       СЧИ   М5
       НТЖ   WОRК
       ПЕ    А00712
       СЧ    temp
       ВЧ    =В'2'
       НТЖ   temp
       НТЖ   -1(М16)
       ЗП    -1(М16)
       И     =В'1777'
       ПО    А00667
       СЧ    loc116(M13)
А00721 И     =В'77777'
       ПЕ    А01160
       СЧ    idx(M13)
       ПО    А01160
       СЧ    (М16)
       ЗП    D00011
       link  А00730,m5
       СЧИ   М5
       ЗП    GОТО
       СЛЦ   =В'1'
       ЗП    SАVМ4
       ПБ    А01160
*******************************
А00730 ПБ    А01242
*******************************
А00731 СЧ    D00011
       do    УИА,(М16),loc50
       МОД   idx(M13)
       МОД   array(M13)
       ЗП    (М16)
       do    СЧ,array(M13),idx
       ПБ    А00721
*******************************
FRЕЕ   call  totlen,m5
А00737 СЧ    1(М16)
       СДА   64+10
       И     =В'77777'
       ЗП    temp
       СЧ    loc116(M13)
       УИ    М5
А00742 НТЖ   temp
       ПО    А00747
       МОДА  (М5)
       СЧ    2(М16)
       МОДА  (М5)
       ЗП    1(М16)
       СЛИА  1(М5)
       СЧИ   М5
       ПБ    А00742
*******************************
А00747 СЧ    loc220(M13)
       И     =В'1777'
       ЗП    D00031
А00751 СЛИА  -1(М5)
       МОДА   А00760
       ПИО   (М5)
       МОДА  (М5)
       СЧ    1(М16)
       И     =В'1777'
       ВЧ    D00031
       ПО    А00751
       МОДА  (М5)
       СЧ    1(М16)
       СЛЦ   curpos(M13)
       МОДА  (М5)
       ЗП    1(М16)
       ПБ    А00751
*******************************
А00760 СЧ    1(М16)
       И     =В'1777'
       ЗП    WОRК
       НТЖ   D00031
       ПО    А00773
       СЧ    D00031
       ВЧ    WОRК
       ПЕ    А00500
       УИ    М5
       СЧ    WОRК
       СЛЦ   curbuf(M13)
       ЗП    WОRК
       СЛЦ   curpos(M13)
       ЗП    D00031
А00767 МОД   WОRК
       СЧ    (М5)
       МОД   D00031
       ЗП    (М5)
       cycle А00767,m5
А00773 СЧ    1(М16)
       ВЧ    =В'2000'
       СЛЦ   curpos(M13)
       ЗП    1(М16)
       СЧ    frebas(M13)
       УИ    М16
* увеличиваем кол-во св. места       
       do    СЧ,(М16),which
       СЛЦ   curpos(M13)
       СЛЦ   =В'1'
       do    ЗП,(М16),which
       dirty 1
       СЧ    loc220(M13)
А01005 СДА   64+20
       И     =В'1777777'
       ПЕ    FRЕЕ
       СЧ    =В'1'
       ИЛИ   dirty(M13)
       ЗП    dirty(M13)
       ПБ    (М6)
********************
DАТЕ   СЧ    loc14(M13)
       И     =М15В'777777'
       ЗП    WОRК
       Э50   ’114’
       СБР   =М20В'77777'
       ИЛИ   WОRК
       ИЛИ   mylen(M13)
       ЗП    datlen
       ПБ    (М7)
********************
FLUSН  ЗП    USRLОС
       call  РR1022,m6
       СЧ    D00024
       ЗП    loc12(M13)
       ПБ    СМD0
***************************
РR1022 call  DАТЕ,m7
А01023 СЧ
       УИА   0(М5)
       ЗП    D00030
       СЧ    frebas(M13)
       УИ    М16
       do    СЧ,(М16),which
       ЗП    WОRК
       ++    mylen(M13)
       ВЧ    WОRК
       ПО    nospac нет места в зоне
       СЧ    which(M13)
chunk  call  getzon,m7 после этого m16 = curbuf
       УИИ   М7(М16)
       СЧ    1(М7)
       СДА   64+10
       УИ    М16
А01035 СДА   64-39
       do    НТЖ,1(М16),curbuf
       И     =М39В'777'
       ПО    А01044
       do    СЧ,1(М16),curbuf
       do    ЗП,2(М16),curbuf
       СЛИА  -1(М16)
       СЧИ   М16
       МОДА  А01035
       ПИНО  (М16)
А01044 СЛИА  1(М16)
       СЧИ   М16
       ЗП    loc116(M13)
       СДА   64-39
       ИЛИ   D00030
РR1047 ЗП    D00030
       СДА   64+39
       СДА   64-10
       ИЛИ   which(M13)
       ЗП    D00024
       СЧ    mylen(M13)
       УИ    М16
       ВЧОБ  1(М7)
       СЛЦ   =В'2000'
       ЗП    1(М7)
       И     =В'1777'
       ЗП    WОRК
       СЛЦ   curbuf(M13)
       СЛЦ   =В'1'
       ЗП    temp
       МОДА  А01062
       ПИНО  (М5)
       СЛИА  -1(М16)
       ++    temp
А01062 СЧИ   М16
       ПО    А01067 нечего копировать
А01063 МОД   USRLОС      * memcpy
       СЧ    -1(М16)     * [temp] <- [usrloc]
       МОД   temp        * length m16
       ЗП    -1(М16)     *
       cycle А01063,m16  *
А01067 СЧ    mylen(M13)
       СДА   64-10
       ИЛИ   WОRК
       ИЛИ   D00030
       do    ЗП,1(М7),loc116
       do    УИА,(М16),frebas база св. места в зонах
       do    СЧ,(М16),which * уменьшаем
       ВЧ    mylen(M13)     * кол-во
       ВЧ    =В'1'             * свободного
       do    ЗП,(М16),which * места
       МОДА  А01111
       ПИО   (М5)
       dirty 1
       СЧ    D00024
       СДА   64-20
       ЗП    D00030
       СЧ    remlen
       ЗП    mylen(M13)
А01105 cycle А01264,m5
       link  ОVЕRFL,m6
       СЧ    D00030
       ПБ    А01005
***************************
А01111 СЧ    datlen
       МОД   temp
       ЗП    -1
       dirty 2
       ПБ    (М6)
***************************
А01116 СЧ
       ЗП    loc20(M13)
       СЧ    loc12(M13)
       link  ОVЕRFL,m6
       ПБ    FRЕЕ
***************************
СМD26  ЗП    D00011
       СЧ    loc12(M13)
       ЗП    adescr(M13)
       ПБ    А01140
***************************
А01123 do    СЧ,array(M13),idx
       СДА   64+15
А01125 ЗП    D00012
       СДА   64+15
       УИ    М16
       СЧМР
       МОД   SАVМ4
       ПО
       СЧ    D00012
       СЛИА  -1(М16)
       МОДА  А01133
       ПИО   (М16)
       СЛЦ   =В'2'
А01133 ВЧ    =В'1'
       ЗП    D00012
       call  STEP,m6
       СЧ    D00012
       ПБ    А01125
***************************
А01136 СЧ    D00032
       СДА   64+29
       ИЛИ   =М47В'1'
А01140 ЗП    D00024
       СЧ    loc50(M13)
       УИ    М16
       do    СЛЦ,array(M13),idx
       СЛЦ   =В'2'
       УИ    М5
       СЧИ   М5
       ЗП    limit(M13)
       СЧ    -1(М16)
       И     =В'1777'
       УИ    М6
       СЛИ   М6(М16)
* раздвижка на 2 слова        
раздвг СЧИ   М6
       НТЖ   limit(M13)
       ПО    раздв2
       СЧ    -2(М6)
       ЗП    (М6)
       СЧ    -1(М6)
       ЗП    1(М6)
       СЛИА  -2(М6)
       ПБ    раздвг
***************************
раздв2 СЧ    D00011
       ЗП    (М5)
       СЧ    D00024
       ЗП    1(М5)
       СЧ    -1(М16)
       СЛЦ   =В'2'
       ЗП    -1(М16)
А01160 СЧИ   М16
       ВЧ    =В'1'
       ЗП    USRLОС
       СЧ    idx(M13)
       ПЕ    А01167
А01163 СЧ    =В'41'
       ЗП    mylen(M13)
       СЧ    -1(М16)
       НТЖ   =В'40'
       ПО    А01252
       СЧ    loc20(M13)
       ПБ    А01311
***************************
А01167 СЧ    -1(М16)
       И     =В'1777'
       НТЖ   =В'100'
       ПО    А01174
А01171 СЧ    -1(М16)
       И     =В'1777'
       СЛЦ   =В'1'
       ЗП    mylen(M13)
       СЧ    loc246(M13)
       ПБ    А01311
***************************
А01174 СЧ    idx(M13)
       СДА   64-1
       СЛЦ   =В'41'
       ЗП    WОRК
       СЧ    loc55(M13)
       НТЖ   =В'36'
       ПЕ    А01201
       СЧ    WОRК
       СЛЦ   =В'44'
       ЗП    WОRК
А01201 call  РR425,m6
       ВЧ    WОRК
       ПЕ    А01116
       СЧ    loc157(M13)
       ЗП    D00025
       СЧ    loc246(M13)
       СДА   64-29
       ЗП    loc157(M13)
       СЧ    loc117(M13)
       И     =М10В'1777777'
       ИЛИ   loc157(M13)
       ИЛИ   =В'40'
       ЗП    loc157(M13)
       СЧ    USRLОС
       СЛЦ   =В'40'
       ЗП    USRLОС
       СЧ    =В'41'
       ЗП    mylen(M13)
       call  РR1022,m6
       СЧ    D00025
       ЗП    loc157(M13)
       СЧ    loc160(M13)
       ЗП    D00011
       СЧ    loc117(M13)
       ЗП    D00025
       СДА   64+29
       СДА   64-19
       ИЛИ   D00024
       СДА   64-10
       ИЛИ   =В'40'
       ЗП    loc117(M13)
       И     =М10В'1777777'
       СДА   64-19
       ЗП    D00032
       m16=  loc120(M13)
       link  А01231,m5
       СЧИ   М5
       ЗП    GОТО
       СЧИ   М16
       ВЧ    =В'1'
       ЗП    USRLОС
       ПБ    А01171
***************************
А01231 link  А01136,m5
РR1232 СЧИ   М5
       ЗП    SАVМ4
       СЧ    =В'1'
       ЗП    desc2(M13)
       СЧ    D00025
       СДА   64+10
       И     =В'1777777'
       ПО    А01242
       call  А00340,m6
       СДА   64-19
       СДА   64+19
       ИЛИ   D00032
       call  РR662,m6
А01242 m16=  loc56(M13)
       СЧИ   М16
       ЗП    loc50(M13)
       СЧ    idx(M13)
       ПО    drtnxt
       ВЧ    =В'2'
       ЗП    idx(M13)
       НТЖ
       ПО    А01123
       link  А01123,m6
       do    СЧ,loc20(M13),idx
       ПБ    РR202
***************************
А01252 call  РR425,m6
       ВЧ    =В'101'
       ПЕ    А01116
       call  РR1022,m6
       m16=  loc56(M13)
       СЧ    D00024
       ИЛИ   =М47В'1'
       ЗП    1(М16)
       СЧ    =В'2'
       ЗП    -1(М16)
       ПБ    А01163
***************************
long   СЧ    USRLОС
       СЛЦ   mylen(M13)
       ВЧ    =В'1'
       ЗП    USRLОС теперь указ. на посл. слово
       do    УИА,(М5),dblen
А01264 do    СЧ,-1(М5),frebas
       ВЧ    =В'2'
       ПЕ    А01105
       СЛЦ   =В'1'
       ЗП    WОRК
       СЛИА  -1(М5)
       ВЧ    mylen(M13)
       ПЕ    А01274
       СЧ    USRLОС
       ВЧ    mylen(M13)
       СЛЦ   =В'1'
       ЗП    USRLОС вернули на начало
       СЧИ   М5
       УИА   0(М5)
       ПБ    chunk
***************************
А01274 СЧ    mylen(M13)
       ВЧ    WОRК
       ЗП    remlen
       СЧ    WОRК
       ЗП    mylen(M13)
       ВЧОБ  USRLОС
       ЗП    USRLОС
       СЧИ   М5
       СЛИА  1(М5)
       ПБ    chunk
***************************
nospac СЧ    mylen(M13)
       ВЧ    =B'1775'
       ПО    long в одну зону не поместится
* кусок не длинный: ищем зону, в которую он поместится
       УИА   -1(М7)
А01303 СЛИА  1(М7) 
       СЧИ   М7
       НТЖ   dblen(M13)
       ПО    long таких не нашлось, придется дробить
       СЧ    mylen(M13)
       do    ВЧ,(М7),frebas
       ПО    А01303
       СЧИ   М7
       ПБ    chunk нашлась, пишем
***************************
* команда 20: замена значения у тек. записи        
СМD20  ЗП    USRLОС
       СЧ    adescr(M13)
А01311 ЗП    D00012
       link  drtnxt,m6
       call  totlen,m5 найдем общую длину записи
       ВЧ    =В'1'
       УИ    М5
       НТЖ   mylen(M13)
       ПЕ    А01336 старая и новая длины не равны
       МОДА  А01323
       ПИО   (М5) значение нулевой длины
* копирование в существующее место       
А01317 МОД   USRLОС
       СЧ    -1(М5)
       do    ЗП,(М5),aitem
       cycle А01317,m5
А01323 УИИ   М5(М16)
       call  DАТЕ,m7
       do    ЗП,,aitem
       dirty 1
       СЧ    loc220(M13)
       И     =М20В’1777777’
       ПО    RTNEXT
       НТЖ   loc220(M13)
       do    ЗП,1(М5),loc116
       СЧ    loc220(M13)
       ПБ    А01005
***************************
А01336 call  DАТЕ,m7
       do    УИА,(М16),curbuf
       СЧ    1(М16)
       СДА   64+10
       УИ    М5
       СЛИА  1(М5)
       ВЧОБ  1(М16)
       И     =В'1777'
       СЛЦ   curpos(M13)
       ВЧ    =В'2'
       ЗП    WОRК
       ВЧ    mylen(M13)
       ПЕ    А01361
А01346 СЧИ   М6
       ЗП    D00010
       СЧ    loc220(M13)
       ЗП    D00026
       И     =В'1777'
       ЗП    loc220(M13)
       call  А00747,m6
       ++    mylen(M13)
       УИА   0(М5)
       do    УИА,(М7),curbuf
       СЧ    D00026
       И     =М39В'777'
       call  РR1047,m6
       СЧ    D00026
       МОД   D00010
       УИА   (М6)
       ПБ    А01005
***************************
А01361 call  РR425,m6
А01362 do    СЧ,,aitem
       И     =В'77777'
       СЛЦ   itmlen(M13)
       ВЧ    mylen(M13)
       ПЕ    ОVЕRFL 
       do    УИА,(М16),curbuf
       СЧ    WОRК
       ЗП    mylen(M13)
       call  А01346,m6
       СЧ    USRLОС
       СЛЦ   mylen(M13)
       ЗП    USRLОС
       СЧ    datlen
       И     =В'77777'
       ВЧ    mylen(M13)
       ЗП    mylen(M13)
       МОД   USRLОС
       СЧ    -1
       ЗП    datlen
       call  А01023,m6
       СЧ    D00012
       call  totlen,m5
       СЧ    D00024
       СДА   64-20
       ИЛИ   loc220(M13)
       do    ЗП,1(М16),loc116
       dirty 2
       ПБ    RTNEXT
***************************
* команда 34: преобразование дескриптора массива
* в текстовый формат
СМD34  СДА   64-33
       РЗБ   =П’77777’
       ИЛИ   =М40П’ ’
       ЗП    endmrk(M13)
       СЧ    desc1(M13)
       РЗБ   =П’03 1  ’ маска даты
       ИЛИ   =М40П’ ’
       ЗП    desc1(M13)
       СЧ    =M40B'172'M32B'33' ЕОТ
       ЗП    desc2(M13)
       ПБ    СМD0
***************************
СРУОUТ call  РR342,m6
А01415 link  СМD0,m6
       СЧ    myloc(M13)
       ЗП    USRLОС
       СЧ    mylen(M13) если макс. длина указана
       ПЕ    СНКLЕN ее нужно проверить
enough ++    aitem(M13)
       --    curpos(M13)
А01423 УИ    М16
       СЧИ   М16
       ПО    А01431
СОLООР do    СЧ,-1(М16),aitem
       МОД   USRLОС
       ЗП    -1(М16)
       cycle СОLООР,m16
А01431 СЧ    USRLОС
       СЛЦ   curpos(M13)
       ЗП    USRLОС
       СЧ    loc220(M13)
       И     =М20В’1777777’
       ПО    (М6) закончили перепись
       СДА   64+20
       ПБ    А00530 следующий сегмент
*******************************
СНКLЕN ВЧ    itmlen(M13)
       ПО    enough
       УИА   12(М16) длина записи больше длины в памяти
       ПБ    ЕRR
***************************
* команда 10: создание массива
МКСТL  СЧ    dbdesc(M13) дддднузззз
       СДА   64+18  осталась длина
       ЗП    dblen(M13)  сохраним
       УИ    М5
       СЧ    dbdesc(M13) 
       И     =В'777777'  осталось нузззз
       ЗП    e70msk(M13)
       СЧ    bdtab(M13)
       УИ    М6
       ЗП    curbuf(M13) = bdtab
       СДА   64-20  в поле номера листа
       ИЛИ   e70msk(M13)
       ЗП    temp сделали заготовку ис э70
       СЧ    =В'1777'  01777
       ЗП    1(М6)  в слово 1 bdtab
А01447 СЛИА  -1(М5)
* формируем таблицу кол-ва свободных слов
       СЧ    =B'1776' 
       do    ЗП,(М5),bdbuf
       СЧИ   М5    номер зоны массива
       ИЛИ   rootfl(M13)
       ЗП    (М6)  в слово 0 bdtab
       СЧИ   М5
       СЛЦ   temp
       ЗП    ИСЭ70
       Э70   ИСЭ70
       МОДА  А01447
       ПИНО  (М5)
       СЧ    bdbuf(M13)
       ЗП    frebas(M13)
       ЗП    myloc(M13)
       СЧ    =В'2000'   02000
       ЗП    loc20(M13)
       СЧ
       ЗП    D00011
       call  РR376,m6
       call  РR1022,m6 пишем метаданные
       СЧ    dblen(M13)
       ЗП    mylen(M13)
       ВЧОБ  =B'1731'
       do    ЗП,,bdbuf
       УИА   '21'(М5) уход на команду 21
       ПБ    А00153
*******************************
МSG    КОНД  П’НУЛ.УК’
       КОНД  П’ЛЮЧ  0’В’172’
* 2
       КОНД  П’ЗАТЕРТ’
       КОНД  П’ ЛИСТ0’В’172’
* 3
       КОНД  П’НЕТ ЗА’
       КОНД  П’ПИСИ 0’В’172’
* 4
       КОНД  П’НЕДОП.’
       КОНД  П’ ИМЯ.0’В’172’
* 5
       КОНД  П’ЗАТЕРТ’
       КОНД  П’ 1 Л.0’В’172’
* 6
       КОНД  П’ПЕРЕПО’
       КОНД  П’ЛНЕНА0’В’172’
* 7
       КОНД  П’ШАГЗ. ’
       КОНД  П’ВЕЛИК0’В’172’
* 8
       КОНД  П’НЕТ ИМ’
       КОНД  П’ЕНИ  0’В’172’
* 9
       КОНД  П’ИМ.УЖЕ’
       КОНД  П’ ЕСТЬ0’В’172’
* 10
       КОНД  П’НЕТ С.’
       КОНД  П’КОНЦА0’В’172’
* 11
       КОНД  П’СИСТ.О’
       КОНД  П’ШИБКА0’В’172’
* 12
       КОНД  П’ДЛ.ЗАП’
       КОНД  П’>ДЛИН0’В’172’
* 13
       КОНД  П’ОДНОВР’
       КОНД  П’.РАБ.0’В’172’
* 14
       КОНД  П’НЕТ ТЕ’
       КОНД  П’КУЩЕЙ0’В’172’
* 15
       КОНД  П’НЕТ ПР’
       КОНД  П’ЕД.З.0’В’172’
* 16
       КОНД  П’НЕТ СЛ’
       КОНД  П’ЕД.З.0’В’172’
* 17
       КОНД  П’НЕПР.П’
       КОНД  П’АРОЛЬ0’В’172’
ВDVЕСТ decl   1
outadr decl   2
ORGCMD decl   2 командное слово запроса
СURСМD decl   1 текущее значение ком. слова
LOC6   decl   1
givenp decl   1
key    decl   1 ключ запроса
erhndl decl   1
loc12  decl   1
myloc  decl   1
loc14  decl   1
mylen  decl   1
bdbuf  decl   1
bdtab  decl   1
loc20  decl   1
array  decl   7
dbdesc decl   1
rootfl decl   1
savedp decl   1
loc33  decl   1
frebas decl   1 база массива св. слов в bdtab
adescr decl   1 адрес дескриптора текущего эл-та
limit  decl   1
curkey decl   1
endmrk decl   1 
desc1  decl   1 копия дескриптора массива 
desc2  decl   2 
e70msk decl   1
curpos decl   1
aitem  decl   1
itmlen decl   1
loc50  decl   1
dblen  decl   2
loc53  decl   1
loc54  decl   1
loc55  decl   1
loc56  decl   1
       decl   31
loc116 decl   1
loc117 decl   1
loc120 decl   1
       decl   30
loc157 decl   1
loc160 decl   32
loc220 decl   17
curbuf decl   1
idx    decl   2
which  decl   2
loc246 decl   1
dirty  decl   1
       ФИНИШ
eoi$$$
trn$$$
печмак
0-0
бтмалф
наз   010000
зонгп 521000     1
end$$$
*call ficmemory
*     В паспорте 52(1234-wr) - рабочая лента для экспериментов
*libra:13
*stand
*pascal
(*=p-,t-*)PROGRAM main (OUTPUT,BDVECT);
% BDVECT - массив с параметрами задания к МАРСу
% нужно указывать и объявлять, если нужен прямой доступ к нему
type word = record case integer of 0:(i:integer); 1:(a:alfa) end;
var i,j:integer;
a:array [1..1234b] _of integer;
bdvect, sav: array[0..167] of word;
% sav - массив того же типа что bdvect для определения изменившихся слов
% процедуры работы с МАРСом согласно инструкции Пирина
PROCEDURE PASSETAR(I: INTEGER); EXTERNAL; 
PROCEDURE PASACD(VAR I: INTEGER); FORTRAN;
% процедура печати изменившихся слов 
procedure dump;
_var j:integer;
_( 
    for j := 0 to 167 do 
        if bdvect[j] <> sav[j] then
            writeln(' word ', j : 3 oct,
                ' changed from ', sav[j] oct,
                ' to ', bdvect[j] oct, '(', bdvect[j].a, ')');
    sav := bdvect;
_);
% обнуление массива
procedure zero;
_(
	for j := 1 to 1234b do a[j] := 0;

_);
% проверка значений в массиве
procedure check;
_(
	for j := 1 to 1234b do 
	if (a[j] <> j*j) then writeln(' elt ', j, ' is ', a[j],
	' instead of ', j*j);
_);
BEGIN
	sav := bdvect;
% создаем каталог баз данных длиной 1 зону с нулевой зоны
% НУ 52, константа должна быть типа С (без порядка), а не В 
	j := 1520000C;
% процедура PASACD описана как FORTRAN, поэтому параметр передается
% через переменную 
% при работе с существующей базой PASACD нужно убрать
        PASACD(j); writeln(' after pasacd'); dump;
% настраиваемся на каталог, здесь уже можно константу
        PASSETAR(1520000c); writeln(' after passetar'); dump;
% создаем файл данных YYY на НУ 52 с зоны 4 длиной 3 зоны
% здесь тоже константа обязательно типа С
	j := 1270000C;
% для работы с существующим файлом newd нужно убрать
        newd('YYY', j); writeln(' after newd'); dump;
% открываем его
        opend('yyy'); writeln(' after opend'); dump;
% инициализируем массив а
        for j := 1 to 1234b do a[j] := j*j;
% пишем массив в текущий файл
        putd('val', a); writeln(' after putd'); dump;
% обнуляем массив
        zero;
% читаем в массив из файла
        getd('val', a); writeln(' after getd'); dump;
% проверяем, что значения равны ожидаемым
        check;
% изменяем какое-нибудь слово массива
        a[125] := 999999;
% и записываем в файл
        modd('val', a);
% обнуляем, читаем, сравниваем - отличие в одном слове
        zero; getd('val', a); check;
% ещё можно указывать длину записываемого элемента
        modd('val', a:1232b);
% обнуляем, читаем, сравниваем - отличие в трех словах
        zero; getd('val', a); check; dump;
% прочтенная длина 1232 видна в 3 разных словах
END.
*     no load list
*execute
*end file
``````
еконец
